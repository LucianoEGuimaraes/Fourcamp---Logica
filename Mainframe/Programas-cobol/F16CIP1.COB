      *====================================================             00010000
       IDENTIFICATION                            DIVISION.              00020000
      *====================================================             00030000
       PROGRAM-ID. FR16CIP1.                                            00040014
                                                                        00061009
      *====================================================*            00062009
      *   AUTOR....:LUCIANO GUIMARAES                      *            00063009
      *   ANALISTA.:IVAN SANCHES                           *            00063110
      *   DATA ....:22/06/2022                             *            00063214
      *----------------------------------------------------*            00063309
      *   OBJETIVO:INVOCAR UM MAPA BMS CICS RELACIONADO    *            00063414
      *            COM UM BANCO DE DADOS DB2 E REALIZAR    *            00063514
      *            FUNCOES ESCOLHIDAS PELO USUARIOS        *            00063814
      *----------------------------------------------------*            00064010
      *   BASE DE DADOS:                                   *            00066010
      *   TABELA.DB2..                                     *            00067010
      *    ------              I/O        INCLUDE/BOOK     *            00068010
      *====================================================*            00080009
       DATA                                      DIVISION.              00119510
      *====================================================             00119610
       WORKING-STORAGE                           SECTION.               00138001
      *----------------------------------------------------             00138101
                                                                        00138614
            EXEC SQL                                                    00138714
               INCLUDE #BKFUNC                                          00138826
            END-EXEC.                                                   00138914
                                                                        00139014
            EXEC SQL                                                    00139114
               INCLUDE SQLCA                                            00139226
            END-EXEC.                                                   00139314
                                                                        00139414
            COPY F16CIM1.                                               00139514
            COPY DFHAID.                                                00139622
                                                                        00139710
      *----------------------------------------------------             00143610
       01 FILLER              PIC X(70) VALUE                           00143710
              '---------FILE STATUS E DATAS----------'.                 00143828
                                                                        00143927
       01 WRK-DATAADM.                                                  00144027
          05 WRK-DATAANO    PIC X(04).                                  00144127
          05 FILLER         PIC X(01) VALUE '-'.                        00144231
          05 WRK-DATAMES    PIC X(02).                                  00144327
          05 FILLER         PIC X(01) VALUE '-'.                        00144431
          05 WRK-DATADIA    PIC X(02).                                  00144527
                                                                        00144925
       77 WRK-EMAIL-NULL      PIC S9(04) COMP.                          00145014
       77 WRK-SQLCODE         PIC -999.                                 00146010
      *====================================================             00153000
       PROCEDURE                                 DIVISION.              00154000
      *====================================================             00155000
      *****************************************************             00155100
      *          R O T I N A   P R I N C I P A L          *             00155200
      *****************************************************             00155300
      *----------------------------------------------------             00156000
       0000-PRINCIPAL                            SECTION.               00157000
      *----------------------------------------------------             00157100
            PERFORM 1000-INICIALIZAR.                                   00157326
            PERFORM 2000-PROCESSAR.                                     00157526
                                                                        00157614
            EXEC CICS                                                   00157714
              RETURN TRANSID ('T161')                                   00157826
            END-EXEC.                                                   00157914
      *----------------------------------------------------             00158300
       0000-99-FIM.                               EXIT.                 00158400
      *----------------------------------------------------             00158500
      *****************************************************             00162325
      *             I N I C I A L I Z A R                 *             00162425
      *****************************************************             00162525
      *----------------------------------------------------             00166527
       1000-INICIALIZAR                           SECTION.              00166627
      *----------------------------------------------------             00166727
            PERFORM 0500-CICS-SEND.                                     00166827
      *----------------------------------------------------             00166927
       1000-99-FIM.                                 EXIT.               00167027
      *----------------------------------------------------             00167127
      *----------------------------------------------------             00167227
      *****************************************************             00167327
      *               P R O C E S S A R                   *             00167427
      *****************************************************             00167527
      *----------------------------------------------------             00171727
       2000-PROCESSAR                             SECTION.              00171827
      *----------------------------------------------------             00171927
            PERFORM 0600-CICS-RECEIVE.                                  00172127
                                                                        00172227
      *SAIDA:F10*                                                       00172328
            IF EIBAID = ':'                                             00172427
              PERFORM 0400-CICS-RETURN                                  00172528
            END-IF.                                                     00172827
                                                                        00172927
      *CONSULTA:F5*                                                     00173028
            IF EIBAID = '5'                                             00174027
              PERFORM 2100-SELECT-MOVE-SQL                              00174528
            END-IF.                                                     00174627
                                                                        00174729
      *INCLUIR:F6*                                                      00174829
            IF EIBAID = '6'                                             00174929
              PERFORM 2200-INSERT-MOVE-SQL                              00175029
            END-IF.                                                     00175129
                                                                        00175232
      *DELETAR:F7*                                                      00175332
            IF EIBAID = '7'                                             00175432
              PERFORM 2300-DELETE-MOVE-SQL                              00175532
            END-IF.                                                     00175632
                                                                        00175732
            PERFORM 0700-CICS-SEND-FIM.                                 00175832
      *----------------------------------------------------             00175932
       2000-99-FIM.                                EXIT.                00176032
      *----------------------------------------------------             00176106
      *----------------------------------------------------             00176228
       0400-CICS-RETURN                           SECTION.              00176328
      *----------------------------------------------------             00176428
            EXEC CICS                                                   00176528
              RETURN                                                    00176628
            END-EXEC.                                                   00176728
      *----------------------------------------------------             00177328
       0400-99-FIM.                                 EXIT.               00177428
      *----------------------------------------------------             00177528
      *----------------------------------------------------             00177628
       0500-CICS-SEND                             SECTION.              00177728
      *----------------------------------------------------             00177828
            EXEC CICS SEND                                              00177928
               MAPSET('F16CIM1')                                        00178028
               MAP('MAPA01')                                            00178128
                ERASE                                                   00178228
               MAPONLY                                                  00178328
            END-EXEC.                                                   00178428
      *----------------------------------------------------             00178528
       0500-99-FIM.                                 EXIT.               00178628
      *----------------------------------------------------             00178728
      *----------------------------------------------------             00178828
       0600-CICS-RECEIVE                          SECTION.              00178928
      *----------------------------------------------------             00179028
            EXEC CICS RECEIVE                                           00179128
              MAPSET('F16CIM1')                                         00179228
              MAP('MAPA01')                                             00179328
              INTO(MAPA01I)                                             00179428
            END-EXEC.                                                   00179528
      *----------------------------------------------------             00179628
       0600-99-FIM.                                 EXIT.               00179728
      *----------------------------------------------------             00179828
      *----------------------------------------------------             00179928
       0700-CICS-SEND-FIM                         SECTION.              00180028
      *----------------------------------------------------             00180128
            EXEC CICS SEND                                              00180228
               MAPSET('F16CIM1')                                        00180328
               MAP('MAPA01')                                            00180428
               DATAONLY                                                 00180528
               FROM(MAPA01O)                                            00180628
            END-EXEC.                                                   00180728
      *----------------------------------------------------             00180828
       0700-99-FIM.                                 EXIT.               00180928
      *----------------------------------------------------             00181028
      *----------------------------------------------------             00181128
       2100-SELECT-MOVE-SQL                       SECTION.              00181228
      *----------------------------------------------------             00181328
            MOVE IDI TO DB2-ID                                          00182028
                                                                        00190028
            EXEC SQL                                                    00200028
              SELECT ID,NOME,SETOR,SALARIO,DATAADM,EMAIL                00210028
              INTO  :DB2-ID,                                            00220028
                    :DB2-NOME,                                          00230028
                    :DB2-SETOR,                                         00240028
                    :DB2-SALARIO,                                       00250028
                    :DB2-DATAADM,                                       00260028
                    :DB2-EMAIL  :WRK-EMAIL-NULL                         00270028
              FROM IVAN.FUNC                                            00280028
               WHERE ID =:DB2-ID                                        00290028
               FETCH FIRST ROW ONLY                                     00300028
            END-EXEC                                                    00310028
                                                                        00320028
            EVALUATE SQLCODE                                            00330028
              WHEN 0                                                    00340028
               MOVE DB2-NOME       TO NOMEO                             00350028
               MOVE DB2-SETOR      TO SETORO                            00360028
               MOVE DB2-SALARIO    TO SALARIOO                          00370028
               MOVE DB2-DATAADM    TO WRK-DATAADM                       00380028
               MOVE WRK-DATADIA    TO DATADIAO                          00390028
               MOVE WRK-DATAMES    TO DATAMESO                          00400028
               MOVE WRK-DATAANO    TO DATAANOO                          00410028
              IF WRK-EMAIL-NULL    EQUAL 0                              00420028
               MOVE DB2-EMAIL      TO EMAILO                            00430028
              END-IF                                                    00440028
               MOVE 'REGISTRO ENCONTRADO     ' TO MSGO                  00450028
              WHEN 100                                                  00460028
               MOVE 'REGISTRO NAO ENCONTRADO ' TO MSGO                  00470028
              WHEN OTHER                                                00480028
               MOVE SQLCODE TO WRK-SQLCODE                              00490028
               MOVE 'ERRO.:'                   TO MSGO                  00500028
               MOVE WRK-SQLCODE                TO MSGO(08:04)           00510028
            END-EVALUATE.                                               00520028
      *----------------------------------------------------             00530028
       2100-99-FIM.                                EXIT.                00540028
      *----------------------------------------------------             00550028
      *----------------------------------------------------             00560029
       2200-INSERT-MOVE-SQL                       SECTION.              00570029
      *----------------------------------------------------             00580029
            MOVE IDI         TO DB2-ID                                  00590032
            MOVE NOMEI       TO DB2-NOME                                00600032
            MOVE SETORI      TO DB2-SETOR                               00610032
            MOVE SALARIOI    TO DB2-SALARIO                             00650032
            MOVE DATADIAI    TO WRK-DATADIA                             00660032
            MOVE DATAMESI    TO WRK-DATAMES                             00670032
            MOVE DATAANOI    TO WRK-DATAANO                             00671032
            MOVE WRK-DATAADM TO DB2-DATAADM                             00680032
            MOVE EMAILI      TO DB2-EMAIL                               00690032
                                                                        00691030
                                                                        00692030
            EXEC SQL                                                    00693030
              INSERT INTO IVAN.FUNC(ID,NOME,SETOR,SALARIO,DATAADM,      00700030
                                    EMAIL)                              00710030
              VALUES (:DB2-ID,                                          00720030
                      :DB2-NOME,                                        00730030
                      :DB2-SETOR,                                       00740030
                      :DB2-SALARIO,                                     00750030
                      :DB2-DATAADM,                                     00760030
                      :DB2-EMAIL)                                       00770030
            END-EXEC                                                    00780030
                                                                        00781030
            EVALUATE SQLCODE                                            00790030
              WHEN 0                                                    00800030
                 EXEC SQL                                               00810030
                   COMMIT                                               00820030
                 END-EXEC                                               00830030
                 MOVE 'REGISTRO INSERIDO! ' TO MSGO                     00840032
              WHEN OTHER                                                00850030
                 MOVE SQLCODE TO WRK-SQLCODE                            00860030
                 MOVE WRK-SQLCODE TO MSGO                               00870030
            END-EVALUATE.                                               00880030
      *----------------------------------------------------             00940029
       2200-99-FIM.                                EXIT.                00950029
      *----------------------------------------------------             00960029
      *----------------------------------------------------             00970032
       2300-DELETE-MOVE-SQL                       SECTION.              00980032
      *----------------------------------------------------             00990032
            MOVE IDI         TO DB2-ID                                  01000032
                                                                        01100032
            EXEC SQL                                                    01110032
                DELETE FROM IVAN.FUNC WHERE ID = :DB2-ID                01120032
            END-EXEC.                                                   01130032
                                                                        01210032
            EVALUATE SQLCODE                                            01220032
              WHEN 0                                                    01230032
                 MOVE 'REGISTRO DELETADO! ' TO MSGO                     01270032
              WHEN 100                                                  01280032
                 MOVE 'REGISTRO NAO ENCONTRADO! ' TO MSGO               01281032
              WHEN OTHER                                                01282032
                 MOVE SQLCODE TO WRK-SQLCODE                            01290032
                 MOVE WRK-SQLCODE TO MSGO                               01300032
            END-EVALUATE.                                               01310032
      *----------------------------------------------------             01320032
       2300-99-FIM.                                EXIT.                01330032
      *----------------------------------------------------             01340032
