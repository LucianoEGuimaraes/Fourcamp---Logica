      *====================================================             00010000
       IDENTIFICATION                            DIVISION.              00020000
      *====================================================             00030000
       PROGRAM-ID. FR16DB06.                                            00040009
      *====================================================             00061000
      *             T R E I N A M E N T O                 *             00062000
      *====================================================             00063000
      * PROGRAMA......: FR16DB06.                         *             00064009
      *---------------------------------------------------*             00066000
      * PROGRAMADOR...: LUCIANO GUIMARAES.                *             00067000
      * DATA..........: 10/06/2022                        *             00068009
      * EMPRESA.......: FOURSYS                           *             00069000
      * INSTRUTOR.....: IVAN SANCHES                      *             00069100
      *---------------------------------------------------*             00069200
      * OBJETIVO......: ACESSAR A TABELA DB2 FUNC GRAVAR  *             00069309
      *                 INFORMACOES EM UM ARQUIVO         *             00069409
      *                                                   *             00070000
      *----------------------------------------------------             00080000
      *====================================================             00110000
       ENVIRONMENT                               DIVISION.              00120000
      *====================================================             00130000
       CONFIGURATION                             SECTION.               00131000
       SPECIAL-NAMES.                                                   00132000
           DECIMAL-POINT  IS COMMA.                                     00133000
                                                                        00134000
       INPUT-OUTPUT                              SECTION.               00134110
       FILE-CONTROL.                                                    00134209
            SELECT RELDB2 ASSIGN TO RELDB2                              00134309
                FILE STATUS IS WRK-FS-RELDB2.                           00134409
                                                                        00134509
      *====================================================             00134609
       DATA                                      DIVISION.              00134709
      *====================================================             00134809
       FILE                                      SECTION.               00134909
       FD RELDB2                                                        00135009
           RECORDING MODE IS F                                          00135109
           LABEL RECORD IS STANDARD.                                    00135211
                                                                        00135310
       01 FD-RELDB2         PIC X(99).                                  00135410
                                                                        00135510
      *====================================================             00137000
       WORKING-STORAGE                           SECTION.               00138001
      *----------------------------------------------------             00138101
            EXEC SQL                                                    00138210
               INCLUDE #BKFUNC                                          00139010
            END-EXEC.                                                   00139110
                                                                        00139210
            EXEC SQL                                                    00139310
               INCLUDE SQLCA                                            00139410
            END-EXEC.                                                   00139510
                                                                        00139610
            EXEC SQL                                                    00139710
               DECLARE CFUNC CURSOR FOR                                 00139810
                SELECT * FROM IVAN.FUNC                                 00139910
                 ORDER BY ID                                            00140010
            END-EXEC.                                                   00140110
                                                                        00140210
        01 WRK-RELDB2.                                                  00140310
           05 WRK-ID      PIC 99999.                                    00140410
           05 WRK-NOME    PIC X(30).                                    00141010
           05 WRK-SETOR   PIC X(04).                                    00150010
           05 WRK-SALARIO PIC 9999999999.                               00150110
           05 WRK-DATAADM PIC X(10).                                    00150210
           05 WRK-EMAIL   PIC X(40).                                    00150310
      *------------VARIAVEIS DE APOIO----------------------             00152010
        77 WRK-FS-RELDB2   PIC 9(02).                                   00152210
        77 WRK-SQLCODE     PIC -999.                                    00152310
        77 WRK-NULL-EMAIL  PIC S9(04) COMP.                             00152410
        77 WRK-MEDIA       PIC 9(08)V99 COMP VALUE ZEROS.               00152510
      *------------VARIAVEIS ACUMULADORAS------------------             00152610
        77 WRK-REGLIDOS    PIC 9(02)    VALUE ZEROS.                    00152710
        77 WRK-SALACUM     PIC 9(08)V99 VALUE ZEROS.                    00153010
                                                                        00153110
      *====================================================             00153210
       PROCEDURE                                 DIVISION.              00154000
      *====================================================             00155000
      *****************************************************             00155210
      *          R O T I N A   P R I N C I P A L          *             00155310
      *****************************************************             00155410
      *----------------------------------------------------             00156000
       0000-PRINCIPAL                            SECTION.               00157000
      *----------------------------------------------------             00157100
                                                                        00157200
            PERFORM 1000-INICIALIZAR.                                   00157302
            PERFORM 2000-PROCESSAR UNTIL SQLCODE EQUAL 100.             00157507
            PERFORM 3000-FINALIZAR.                                     00157807
            STOP RUN.                                                   00157907
                                                                        00158000
      *----------------------------------------------------             00158100
       0000-99-FIM.                               EXIT.                 00158200
      *----------------------------------------------------             00158300
      *****************************************************             00158400
      *             I N I C I A L I Z A R                 *             00158500
      *****************************************************             00158600
      *----------------------------------------------------             00158700
       1000-INICIALIZAR                           SECTION.              00158800
      *----------------------------------------------------             00158900
                                                                        00159000
            EXEC SQL                                                    00159104
               OPEN CFUNC                                               00159204
            END-EXEC.                                                   00159304
                                                                        00159410
            EVALUATE SQLCODE                                            00159510
              WHEN 0                                                    00159604
                PERFORM 4000-LER-FUNCIONARIO                            00159704
              WHEN 100                                                  00159804
                DISPLAY 'SEM FUNCIONARIOS'                              00159904
              WHEN OTHER                                                00160004
                MOVE SQLCODE TO WRK-SQLCODE                             00160104
                DISPLAY 'ERRO ' WRK-SQLCODE ' NO OPEN CURSOR'           00160204
                STOP RUN                                                00160304
            END-EVALUATE.                                               00160410
                                                                        00160510
            OPEN OUTPUT RELDB2.                                         00160610
            PERFORM 1200-TESTAR-STATUS.                                 00160710
      *----------------------------------------------------             00160800
       1000-99-FIM.                                 EXIT.               00160900
      *----------------------------------------------------             00161000
      *----------------------------------------------------             00161410
       1200-TESTAR-STATUS                         SECTION.              00161510
      *----------------------------------------------------             00161610
            IF WRK-FS-RELDB2 NOT EQUAL 0                                00161710
             DISPLAY 'ERRO NO OPEN DO ARQUIVO'                          00161810
             STOP RUN                                                   00161910
            END-IF.                                                     00162010
      *----------------------------------------------------             00162110
       1200-99-FIM.                                 EXIT.               00162210
      *----------------------------------------------------             00162310
      *----------------------------------------------------             00162410
       4000-LER-FUNCIONARIO                       SECTION.              00162510
      *----------------------------------------------------             00162610
                                                                        00162710
            EXEC SQL                                                    00162810
             FETCH CFUNC                                                00162910
              INTO :DB2-ID,                                             00163010
                   :DB2-NOME,                                           00163110
                   :DB2-SETOR,                                          00163210
                   :DB2-SALARIO,                                        00163310
                   :DB2-DATAADM,                                        00163410
                   :DB2-EMAIL    :WRK-NULL-EMAIL                        00163510
            END-EXEC.                                                   00163610
                                                                        00163710
            EVALUATE SQLCODE                                            00163810
             WHEN 0                                                     00163910
                CONTINUE                                                00164010
                ADD 1 TO WRK-REGLIDOS                                   00164110
                ADD DB2-SALARIO TO WRK-SALACUM                          00164210
             WHEN 100                                                   00164304
                DISPLAY ' '                                             00164404
                DISPLAY 'FIM DE TABELA '                                00164610
                DISPLAY ' '                                             00164710
                DISPLAY 'REGISTROS TOTAIS LIDOS : '   WRK-REGLIDOS      00164810
                DISPLAY 'SOMA DOS SALARIOS      : '  WRK-SALACUM        00164910
                COMPUTE WRK-MEDIA = WRK-SALACUM / WRK-REGLIDOS          00165010
                DISPLAY 'MEDIA DOS SALARIOS     : ' WRK-MEDIA           00165210
             WHEN OTHER                                                 00165310
                MOVE SQLCODE TO  WRK-SQLCODE                            00165410
                DISPLAY ' '                                             00165510
                DISPLAY 'ERRO NA LEITURA' WRK-SQLCODE                   00165610
            END-EVALUATE.                                               00165710
      *----------------------------------------------------             00165810
       4000-99-FIM.                                  EXIT.              00165910
      *----------------------------------------------------             00166010
      *****************************************************             00166110
      *               P R O C E S S A R                   *             00166210
      *****************************************************             00166310
      *----------------------------------------------------             00166410
       2000-PROCESSAR                             SECTION.              00166510
      *----------------------------------------------------             00166610
            INITIALIZE WRK-RELDB2.                                      00166710
            PERFORM 2500-MOVE-DADOS.                                    00167010
                                                                        00168010
            WRITE FD-RELDB2 FROM WRK-RELDB2.                            00168410
                                                                        00168510
            PERFORM 4000-LER-FUNCIONARIO.                               00168610
      *----------------------------------------------------             00168706
       2000-99-FIM.                                EXIT.                00168806
      *----------------------------------------------------             00168906
      *----------------------------------------------------             00169006
       2500-MOVE-DADOS                             SECTION.             00169110
      *----------------------------------------------------             00169206
            MOVE DB2-ID      TO WRK-ID.                                 00169310
            MOVE DB2-NOME    TO WRK-NOME.                               00169410
            MOVE DB2-SETOR   TO WRK-SETOR.                              00169510
            MOVE DB2-SALARIO TO WRK-SALARIO.                            00169610
            MOVE DB2-DATAADM TO WRK-DATAADM.                            00169710
            MOVE DB2-EMAIL   TO WRK-EMAIL.                              00169810
      *----------------------------------------------------             00170706
       2500-99-FIM.                                EXIT.                00170810
      *----------------------------------------------------             00170906
      *****************************************************             00172106
      *               F I N A L I Z A R                   *             00172206
      *****************************************************             00172306
      *----------------------------------------------------             00172406
       3000-FINALIZAR                             SECTION.              00172506
      *----------------------------------------------------             00172606
            EXEC SQL                                                    00172710
               CLOSE CFUNC                                              00175010
            END-EXEC.                                                   00180010
                                                                        00190010
            CLOSE RELDB2.                                               00200010
                                                                        00201004
      *----------------------------------------------------             00210004
       3000-99-FIM.                                  EXIT.              00220004
      *----------------------------------------------------             00230004
