      *====================================================             00010000
       IDENTIFICATION                            DIVISION.              00020000
      *====================================================             00030000
       PROGRAM-ID. FR16CIP2.                                            00040033
                                                                        00061009
      *====================================================*            00062009
      *   AUTOR....:LUCIANO GUIMARAES                      *            00063009
      *   ANALISTA.:IVAN SANCHES                           *            00063110
      *   DATA ....:29/06/2022                             *            00063242
      *----------------------------------------------------*            00063309
      *   OBJETIVO:INVOCAR UM MAPA BMS CICS RELACIONADO    *            00063414
      *            COM UM ARQUIVO VSAM E EXECUTAR AS       *            00063533
      *            FUNCOES ESCOLHIDAS PELO USUARIOS E USO  *            00063841
      *            DE LOGICA PSEUDO DE SAIDA.              *            00063942
      *----------------------------------------------------*            00064010
      *   BASE DE DADOS:                                   *            00066010
      *   TABELA.VSAM                                      *            00067033
      *    ------              I/O        INCLUDE/BOOK     *            00068010
      *====================================================*            00080009
      *====================================================             00081036
       ENVIRONMENT                               DIVISION.              00082036
      *====================================================             00083036
       CONFIGURATION                              SECTION.              00090036
                                                                        00091036
       SPECIAL-NAMES.                                                   00100036
           DECIMAL-POINT IS COMMA.                                      00110036
                                                                        00110136
      *====================================================             00111036
       DATA                                      DIVISION.              00119510
      *====================================================             00119636
       WORKING-STORAGE                           SECTION.               00138001
      *----------------------------------------------------             00138101
                                                                        00139414
            COPY F16CIM1.                                               00139514
            COPY DFHAID.                                                00139622
                                                                        00139710
      *----------------------------------------------------             00143610
       01 FILLER              PIC X(70) VALUE                           00143710
              '---------FILE STATUS E DADOS----------'.                 00143833
                                                                        00144027
       01 WRK-DADOS.                                                    00144133
          05 WRK-CHAVE        PIC X(05).                                00144233
          05 WRK-NOME         PIC X(30).                                00144333
          05 WRK-SETOR        PIC X(04).                                00144433
          05 WRK-SALARIO      PIC X(10).                                00144534
          05 WRK-DATADIA      PIC X(02).                                00144840
          05 FILLER           PIC X(01) VALUE '-'.                      00144940
          05 WRK-DATAMES      PIC X(02).                                00145040
          05 FILLER           PIC X(01) VALUE '-'.                      00145140
          05 WRK-DATAANO      PIC X(04).                                00145240
          05 WRK-EMAIL        PIC X(40).                                00145333
                                                                        00145933
       77 WRK-RESP            PIC S9(04) COMP.                          00146037
       77 WRK-SAL-ED          PIC ZZ.ZZZ.ZZ9,99.                        00146135
       77 WRK-RET-MAPA        PIC S9(04) COMP.                          00146241
       77 WRK-FLAG            PIC 9(01).                                00146341
       77 WRK-FIM             PIC X(30) VALUE '--PROGRAMA ENCERRADO--'. 00146441
       77 WRK-FASE            PIC 9(02).                                00146542
                                                                        00146634
      *====================================================             00153000
       PROCEDURE                                 DIVISION.              00154000
      *====================================================             00155000
      *****************************************************             00155100
      *          R O T I N A   P R I N C I P A L          *             00155200
      *****************************************************             00155300
      *----------------------------------------------------             00156000
       0000-PRINCIPAL                            SECTION.               00157000
      *----------------------------------------------------             00157100
            PERFORM 1000-INICIALIZAR.                                   00157326
            IF EIBCALEN NOT EQUAL 1                                     00157443
              PERFORM 2000-PROCESSAR                                    00157542
            END-IF.                                                     00157641
            PERFORM 3000-FINALIZAR.                                     00157741
                                                                        00157941
            EXEC CICS                                                   00158014
              RETURN TRANSID ('T162')                                   00158133
            END-EXEC.                                                   00158214
      *----------------------------------------------------             00158300
       0000-99-FIM.                               EXIT.                 00158400
      *----------------------------------------------------             00158500
      *****************************************************             00162325
      *             I N I C I A L I Z A R                 *             00162425
      *****************************************************             00162525
      *----------------------------------------------------             00166527
       1000-INICIALIZAR                           SECTION.              00166627
      *----------------------------------------------------             00166727
            IF EIBCALEN EQUAL 0                                         00166842
                EXEC CICS SEND                                          00166942
                   MAPSET('F16CIM1')                                    00167042
                   MAP('MAPA01')                                        00167142
                    ERASE                                               00167242
                   MAPONLY                                              00167342
                END-EXEC                                                00167442
             MOVE 1 TO WRK-FASE                                         00167542
            END-IF.                                                     00167642
      *----------------------------------------------------             00168927
       1000-99-FIM.                                 EXIT.               00169027
      *----------------------------------------------------             00169127
      *----------------------------------------------------             00169227
      *****************************************************             00169327
      *               P R O C E S S A R                   *             00169427
      *****************************************************             00170027
      *----------------------------------------------------             00171727
       2000-PROCESSAR                             SECTION.              00171827
      *----------------------------------------------------             00171927
            EXEC CICS RECEIVE                                           00172042
               MAPSET('F16CIM1')                                        00172142
               MAP('MAPA01')                                            00172242
               INTO(MAPA01I)                                            00172342
               RESP(WRK-RET-MAPA)                                       00172442
            END-EXEC.                                                   00172542
                                                                        00172642
            EVALUATE EIBAID                                             00172741
                                                                        00172827
      *SAIDA:F10*                                                       00172928
            WHEN ':'                                                    00173041
              EXEC CICS SEND                                            00173141
               FROM(WRK-FIM)                                            00173241
               ERASE                                                    00173341
              END-EXEC                                                  00173441
                                                                        00173541
              EXEC CICS                                                 00173641
                RETURN                                                  00173741
              END-EXEC                                                  00173841
                                                                        00173927
      *CONSULTA:F5*                                                     00174028
            WHEN '5'                                                    00174141
              PERFORM 2100-READ-MOVE-VSAM                               00174533
                                                                        00174729
      *INCLUIR:F6*                                                      00174829
      *     IF EIBAID = '6'                                             00174933
      *       PERFORM 2200-INSERT-MOVE-VSAM                             00175033
      *     END-IF.                                                     00175133
                                                                        00175232
      *DELETAR:F7*                                                      00175332
      *     IF EIBAID = '7'                                             00175433
      *       PERFORM 2300-DELETE-MOVE-VSAM                             00175533
      *     END-IF.                                                     00175633
                                                                        00175732
            WHEN ''''                                                   00175841
             INITIALIZE MAPA01O                                         00175942
            END-EVALUATE.                                               00176241
      *----------------------------------------------------             00176732
       2000-99-FIM.                                EXIT.                00176832
      *----------------------------------------------------             00176906
      *----------------------------------------------------             00181128
       2100-READ-MOVE-VSAM                        SECTION.              00181233
      *----------------------------------------------------             00181328
            MOVE IDI TO WRK-CHAVE                                       00181433
                                                                        00181533
            EXEC CICS READ                                              00181633
                      FILE('FUNC')                                      00181733
                      RIDFLD(WRK-CHAVE)                                 00181833
                      INTO(WRK-DADOS)                                   00181933
                      RESP(WRK-RESP)                                    00182037
            END-EXEC                                                    00182133
                                                                        00182233
            EVALUATE WRK-RESP                                           00182337
              WHEN DFHRESP(NORMAL)                                      00183034
                MOVE WRK-NOME     TO NOMEO                              00184034
                MOVE WRK-SETOR    TO SETORO                             00185034
                MOVE WRK-SALARIO  TO WRK-SAL-ED                         00185234
                MOVE WRK-SAL-ED   TO SALARIOO                           00185334
                MOVE WRK-DATADIA  TO DATADIAO                           00185434
                MOVE WRK-DATAMES  TO DATAMESO                           00185534
                MOVE WRK-DATAANO  TO DATAANOO                           00185634
                MOVE WRK-EMAIL    TO EMAILO                             00188034
                 IF WRK-SALARIO EQUAL ZEROS OR                          00189039
                    WRK-SALARIO NOT NUMERIC                             00189138
                   MOVE '**ATENCAO** INCONSISTENCIA NO SALARIO' TO MSGO 00190034
                 ELSE                                                   00200034
                   MOVE 'FUNCIONARIO ENCONTRADO' TO MSGO                00210034
                 END-IF                                                 00220034
              WHEN DFHRESP(NOTFND)                                      00240034
                MOVE 'FUNCIONARIO INEXISTENTE' TO MSGO                  00250034
              WHEN OTHER                                                00251034
                MOVE 'ERRO ' TO MSGO                                    00252034
                MOVE WRK-RESP TO MSGO(08:04)                            00253037
            END-EVALUATE.                                               00260034
      *----------------------------------------------------             00530028
       2100-99-FIM.                                EXIT.                00540028
      *----------------------------------------------------             00550028
      *----------------------------------------------------             00560041
       3000-FINALIZAR                             SECTION.              00570041
               EXEC CICS SEND                                           00571042
                  MAPSET('F16CIM1')                                     00572042
                  MAP('MAPA01')                                         00573042
                  DATAONLY                                              00574042
                  FROM(MAPA01O)                                         00575042
               END-EXEC.                                                00576042
      *----------------------------------------------------             00580041
      *----------------------------------------------------             00710041
       3000-99-FIM.                                 EXIT.               00720042
      *----------------------------------------------------             00730041
