      *====================================================             00010000
       IDENTIFICATION                            DIVISION.              00020000
      *====================================================             00030000
       PROGRAM-ID. FR16DB07.                                            00040009
      *====================================================             00061000
      *             T R E I N A M E N T O                 *             00062000
      *====================================================             00063000
      * PROGRAMA......: FR16DB07.                         *             00064009
      *---------------------------------------------------*             00066000
      * PROGRAMADOR...: LUCIANO GUIMARAES.                *             00067000
      * DATA..........: 13/06/2022                        *             00068009
      * EMPRESA.......: FOURSYS                           *             00069000
      * INSTRUTOR.....: IVAN SANCHES                      *             00069100
      *---------------------------------------------------*             00069200
      * OBJETIVO......: ACESSAR A TABELA DB2 FUNC E LER   *             00069302
      *                 TODOS OS REGISTROS REALIZANDO UM  *             00069409
      *                 JOIN COM O  CAMPO SETOR           *             00070009
      *----------------------------------------------------             00080000
      *====================================================             00110000
       ENVIRONMENT                               DIVISION.              00120000
      *====================================================             00130000
       CONFIGURATION                             SECTION.               00131000
       SPECIAL-NAMES.                                                   00132000
           DECIMAL-POINT  IS COMMA.                                     00133000
                                                                        00133109
       INPUT-OUTPUT                                 SECTION.            00133209
       FILE-CONTROL.                                                    00133309
            SELECT RELSEC ASSIGN TO RELSEC                              00133409
                FILE STATUS IS WRK-FS-RELSEC.                           00133509
                                                                        00134000
      *====================================================             00135000
       DATA                                      DIVISION.              00136000
      *====================================================             00137000
       FILE                                      SECTION.               00137109
      *----------------------------------------------------             00137209
       FD RELSEC                                                        00137310
           RECORDING MODE IS F                                          00137409
           LABEL RECORD IS STANDARD                                     00137509
           BLOCK CONTAINS 0 RECORDS.                                    00137609
                                                                        00137709
       01 FD-RELSEC         PIC X(135).                                 00137810
      *----------------------------------------------------             00137909
       WORKING-STORAGE                           SECTION.               00138001
      *----------------------------------------------------             00138309
                                                                        00138409
           EXEC SQL                                                     00139001
              INCLUDE #BKFUNC                                           00139109
           END-EXEC.                                                    00139209
                                                                        00139309
           EXEC SQL                                                     00139409
              INCLUDE #BKSETOR                                          00139509
           END-EXEC.                                                    00139609
                                                                        00139709
           EXEC SQL                                                     00139809
              INCLUDE SQLCA                                             00139909
           END-EXEC.                                                    00140009
                                                                        00140109
           EXEC SQL                                                     00140209
              DECLARE CFUNC CURSOR FOR                                  00140309
               SELECT ID,NOME,SALARIO,DATAADM,EMAIL,DESCSETOR           00140409
                FROM IVAN.FUNC F , IVAN.SETOR S                         00140509
                 WHERE F.SETOR = S.IDSETOR                              00140609
           END-EXEC.                                                    00140709
                                                                        00140809
       01 WRK-DADOS.                                                    00140909
          05 WRK-ID        PIC 99999.                                   00141009
          05 WRK-NOME      PIC X(30).                                   00141109
          05 WRK-SALARIO   PIC 9999999999.                              00141309
          05 WRK-DATAADM   PIC X(10).                                   00141409
          05 WRK-EMAIL     PIC X(40).                                   00141509
          05 WRK-DESCSETOR PIC X(40).                                   00141609
                                                                        00142001
      *----------------VARIAVEIS DE APOIO------------------             00142104
       77 WRK-SQLCODE                   PIC -999.                       00143002
       77 WRK-NULL-EMAIL                PIC S9(4) COMP.                 00150002
       77 WRK-MEDIA                     PIC 9(08)V99 COMP.              00150106
       77 WRK-FS-RELSEC                 PIC 9(02).                      00150209
      *----------------VARIAVEIS ACUMULADORAS--------------             00150306
       77 WRK-REG-LIDOS                 PIC 9(02) VALUE ZEROS.          00152006
      *====================================================             00153000
       PROCEDURE                                 DIVISION.              00154000
      *====================================================             00155000
      *****************************************************             00155100
      *          R O T I N A   P R I N C I P A L          *             00155200
      *****************************************************             00155300
      *----------------------------------------------------             00156000
       0000-PRINCIPAL                            SECTION.               00157000
      *----------------------------------------------------             00157100
                                                                        00157200
            PERFORM 1000-INICIALIZAR.                                   00157302
            PERFORM 2000-PROCESSAR UNTIL SQLCODE EQUAL 100.             00157507
            PERFORM 3000-FINALIZAR.                                     00157807
            STOP RUN.                                                   00157907
                                                                        00158000
      *----------------------------------------------------             00158100
       0000-99-FIM.                               EXIT.                 00158200
      *----------------------------------------------------             00158300
      *****************************************************             00158400
      *             I N I C I A L I Z A R                 *             00158500
      *****************************************************             00158600
      *----------------------------------------------------             00158700
       1000-INICIALIZAR                           SECTION.              00158800
      *----------------------------------------------------             00158900
            EXEC SQL                                                    00159104
               OPEN CFUNC                                               00159204
            END-EXEC.                                                   00159304
                                                                        00159409
            EVALUATE SQLCODE                                            00159509
              WHEN 0                                                    00159604
                PERFORM 4000-LER-FUNCIONARIO                            00159704
              WHEN 100                                                  00159804
                DISPLAY 'SEM FUNCIONARIOS'                              00159904
              WHEN OTHER                                                00160004
                MOVE SQLCODE TO WRK-SQLCODE                             00160104
                DISPLAY 'ERRO ' WRK-SQLCODE ' NO OPEN CURSOR'           00160204
                STOP RUN                                                00160309
            END-EVALUATE.                                               00160409
                                                                        00160509
            OPEN OUTPUT RELSEC.                                         00160610
            PERFORM 1200-TESTA-STATUS.                                  00160709
      *----------------------------------------------------             00160800
       1000-99-FIM.                                 EXIT.               00160900
      *----------------------------------------------------             00161000
      *----------------------------------------------------             00161109
       1200-TESTA-STATUS                         SECTION.               00161209
      *----------------------------------------------------             00161309
            IF WRK-FS-RELSEC NOT EQUAL 0                                00161510
              DISPLAY ' ERRO NO OPEN DO ARQUIVO'                        00161609
              STOP RUN                                                  00161709
            END-IF.                                                     00161809
      *----------------------------------------------------             00163209
       1200-99-FIM.                                 EXIT.               00163309
      *----------------------------------------------------             00163409
      *----------------------------------------------------             00163509
       4000-LER-FUNCIONARIO                       SECTION.              00163609
      *----------------------------------------------------             00163709
            EXEC SQL                                                    00163909
             FETCH CFUNC                                                00164009
              INTO :DB2-ID,                                             00164109
                   :DB2-NOME,                                           00164209
                   :DB2-SALARIO,                                        00164309
                   :DB2-DATAADM,                                        00164409
                   :DB2-EMAIL     :WRK-NULL-EMAIL,                      00164509
                   :DB2-DESCSETOR                                       00164609
            END-EXEC.                                                   00164709
                                                                        00164809
            EVALUATE SQLCODE                                            00164909
             WHEN 0                                                     00165009
                CONTINUE                                                00165109
             WHEN 100                                                   00165209
                DISPLAY ' '                                             00165309
                DISPLAY 'FIM DE TABELA ' DB2-ID                         00165409
                DISPLAY 'REGISTROS LIDOS: ' WRK-REG-LIDOS               00165509
             WHEN OTHER                                                 00165609
                MOVE SQLCODE TO  WRK-SQLCODE                            00165709
                DISPLAY ' '                                             00165809
                DISPLAY 'ERRO NA LEITURA' WRK-SQLCODE                   00165909
            END-EVALUATE.                                               00166009
      *----------------------------------------------------             00166109
       4000-99-FIM.                                  EXIT.              00166209
      *----------------------------------------------------             00166309
      *****************************************************             00166409
      *               P R O C E S S A R                   *             00166509
      *****************************************************             00166609
      *----------------------------------------------------             00166709
       2000-PROCESSAR                             SECTION.              00166809
      *----------------------------------------------------             00166909
            PERFORM 4500-MOVE-DADOS.                                    00167109
            WRITE FD-RELSEC FROM WRK-DADOS.                             00167209
                                                                        00167409
            ADD 1 TO WRK-REG-LIDOS.                                     00167509
                                                                        00167609
            PERFORM 4000-LER-FUNCIONARIO.                               00168009
                                                                        00168106
                                                                        00168606
      *----------------------------------------------------             00168706
       2000-99-FIM.                                EXIT.                00168806
      *----------------------------------------------------             00168906
      *----------------------------------------------------             00169006
       4500-MOVE-DADOS                             SECTION.             00169109
      *----------------------------------------------------             00169206
            INITIALIZE WRK-DADOS.                                       00169309
            MOVE DB2-ID        TO WRK-ID.                               00169409
            MOVE DB2-NOME      TO WRK-NOME.                             00169509
            MOVE DB2-SALARIO   TO WRK-SALARIO.                          00169609
            MOVE DB2-DATAADM   TO WRK-DATAADM.                          00169709
                                                                        00169909
            IF WRK-NULL-EMAIL = 0                                       00170009
              MOVE DB2-EMAIL   TO WRK-EMAIL                             00170109
            ELSE                                                        00170209
              MOVE 'NULL' TO WRK-EMAIL                                  00170309
            END-IF.                                                     00170409
            MOVE DB2-DESCSETOR TO WRK-DESCSETOR.                        00170509
      *----------------------------------------------------             00170706
       4500-99-FIM.                                EXIT.                00170806
      *----------------------------------------------------             00170906
      *****************************************************             00172106
      *               F I N A L I Z A R                   *             00172206
      *****************************************************             00172306
      *----------------------------------------------------             00172406
       3000-FINALIZAR                             SECTION.              00172506
      *----------------------------------------------------             00172606
            EXEC SQL                                                    00180004
              CLOSE CFUNC                                               00190009
            END-EXEC.                                                   00200004
            CLOSE RELSEC.                                               00201009
      *----------------------------------------------------             00210004
       3000-99-FIM.                                  EXIT.              00220004
      *----------------------------------------------------             00230004
