      *====================================================             00010000
       IDENTIFICATION                            DIVISION.              00020000
      *====================================================             00030000
       PROGRAM-ID. FR16DB05.                                            00040012
      *====================================================             00061000
      *             T R E I N A M E N T O                 *             00062000
      *====================================================             00063000
      * PROGRAMA......: FR16DB05.                         *             00064012
      *---------------------------------------------------*             00066000
      * PROGRAMADOR...: LUCIANO GUIMARAES.                *             00067000
      * DATA..........: 09/06/2022                        *             00068012
      * EMPRESA.......: FOURSYS                           *             00069000
      * INSTRUTOR.....: IVAN SANCHES                      *             00069100
      *---------------------------------------------------*             00069200
      * OBJETIVO......: ACESSAR A TABELA DB2 FUNC, E      *             00069309
      *                 ATUALIZAR UM REGISTRO             *             00069412
      *                                                   *             00070000
      *----------------------------------------------------             00080000
      *====================================================             00110000
       ENVIRONMENT                               DIVISION.              00120000
      *====================================================             00130000
       CONFIGURATION                             SECTION.               00131000
       SPECIAL-NAMES.                                                   00132000
           DECIMAL-POINT  IS COMMA.                                     00133000
                                                                        00134000
      *====================================================             00135000
       DATA                                      DIVISION.              00136000
      *====================================================             00137000
       WORKING-STORAGE                           SECTION.               00138001
      *----------------------------------------------------             00138101
                                                                        00138201
           EXEC SQL                                                     00139001
              INCLUDE #BKFUNC                                           00139101
           END-EXEC.                                                    00139201
                                                                        00139301
           EXEC SQL                                                     00139401
              INCLUDE SQLCA                                             00139501
           END-EXEC.                                                    00139601
                                                                        00139701
      *----------------VARIAVEIS DE APOIO------------------             00139803
       77 WRK-SQLCODE         PIC -999.                                 00140006
                                                                        00141013
       01 WRK-ID.                                                       00150013
          02 FILLER          PIC X(10).                                 00150113
          02 WRK-ID-AC       PIC 9(05).                                 00150213
                                                                        00150313
       01 WRK-NOME.                                                     00150413
          02 FILLER          PIC X(10).                                 00150513
          02 WRK-NOME-AC     PIC X(30).                                 00150613
                                                                        00150713
       01 WRK-SETOR.                                                    00150813
          02 FILLER          PIC X(10).                                 00150913
          02 WRK-SETOR-AC    PIC X(04).                                 00151013
                                                                        00151113
       01 WRK-SALARIO.                                                  00151213
          02 FILLER          PIC X(10).                                 00151313
          02 WRK-SALARIO-AC  PIC 9(08)V99.                              00151413
                                                                        00151513
       01 WRK-DATAADM.                                                  00151613
          02 FILLER          PIC X(10).                                 00151713
          02 WRK-DATAADM-AC  PIC X(10).                                 00151813
                                                                        00151913
       01 WRK-EMAIL.                                                    00152013
          02 FILLER          PIC X(10).                                 00152113
          02 WRK-EMAIL-AC    PIC X(40).                                 00152213
                                                                        00153003
      *====================================================             00154300
       PROCEDURE                                 DIVISION.              00154400
      *====================================================             00155000
      *****************************************************             00155100
      *          R O T I N A   P R I N C I P A L          *             00155200
      *****************************************************             00155300
      *----------------------------------------------------             00156000
       0000-PRINCIPAL                            SECTION.               00157000
      *----------------------------------------------------             00157100
                                                                        00157200
            PERFORM 1000-INICIALIZAR.                                   00157302
             IF WRK-ID-AC IS NUMERIC AND WRK-ID-AC GREATER 0            00157413
               PERFORM 2000-PROCESSAR                                   00157513
             ELSE                                                       00157613
               DISPLAY 'ID INVALIDO'                                    00157713
             END-IF.                                                    00157813
            PERFORM 3000-FINALIZAR.                                     00157905
            STOP RUN.                                                   00158005
                                                                        00158100
      *----------------------------------------------------             00158200
       0000-99-FIM.                               EXIT.                 00158300
      *----------------------------------------------------             00158400
      *****************************************************             00158500
      *             I N I C I A L I Z A R                 *             00158600
      *****************************************************             00158700
      *----------------------------------------------------             00158800
       1000-INICIALIZAR                           SECTION.              00158900
      *----------------------------------------------------             00159000
                                                                        00159100
             ACCEPT WRK-ID.                                             00159213
             ACCEPT WRK-NOME.                                           00159313
             ACCEPT WRK-SETOR.                                          00159413
             ACCEPT WRK-SALARIO.                                        00159513
             ACCEPT WRK-DATAADM.                                        00159613
             ACCEPT WRK-EMAIL.                                          00159713
                                                                        00159806
      *----------------------------------------------------             00159900
       1000-99-FIM.                                 EXIT.               00160000
      *----------------------------------------------------             00160100
      *****************************************************             00160200
      *               P R O C E S S A R                   *             00160300
      *****************************************************             00160400
      *----------------------------------------------------             00160500
       2000-PROCESSAR                             SECTION.              00160600
      *----------------------------------------------------             00160700
            MOVE WRK-ID-AC TO DB2-ID.                                   00160813
                                                                        00160910
            EXEC SQL                                                    00161010
            SELECT ID,NOME,SETOR,SALARIO,DATAADM,EMAIL                  00161113
              INTO :DB2-ID,                                             00161213
                   :DB2-NOME,                                           00161313
                   :DB2-SETOR,                                          00161413
                   :DB2-SALARIO,                                        00161513
                   :DB2-DATAADM,                                        00161613
                   :DB2-EMAIL                                           00161713
               FROM  IVAN.FUNC                                          00161813
              WHERE ID = :DB2-ID                                        00161913
            END-EXEC.                                                   00162013
                                                                        00163213
            IF WRK-NOME-AC NOT EQUAL DB2-NOME                           00163313
              MOVE WRK-NOME-AC TO DB2-NOME                              00163413
            END-IF.                                                     00163513
            IF WRK-SETOR-AC NOT EQUAL DB2-SETOR                         00163613
              MOVE WRK-SETOR-AC TO DB2-SETOR                            00163713
            END-IF.                                                     00163813
            IF WRK-SALARIO-AC NOT EQUAL DB2-SALARIO                     00163913
              MOVE WRK-SALARIO-AC TO DB2-SALARIO                        00164013
            END-IF.                                                     00164113
            IF WRK-DATAADM-AC NOT EQUAL DB2-DATAADM                     00164213
              MOVE WRK-DATAADM-AC TO DB2-DATAADM                        00164313
            END-IF.                                                     00164413
            IF WRK-EMAIL-AC NOT EQUAL DB2-EMAIL                         00164513
              MOVE WRK-EMAIL-AC TO DB2-EMAIL                            00164613
            END-IF.                                                     00164713
                                                                        00164813
            EXEC SQL                                                    00164913
             UPDATE IVAN.FUNC SET NOME    = :DB2-NOME,                  00165013
                                  SETOR   = :DB2-SETOR,                 00165113
                                  SALARIO = :DB2-SALARIO,               00165213
                                  DATAADM = :DB2-DATAADM,               00165313
                                  EMAIL   = :DB2-EMAIL                  00165413
                          WHERE ID = :DB2-ID                            00165513
            END-EXEC.                                                   00165613
                                                                        00165713
            EVALUATE SQLCODE                                            00165813
             WHEN 0                                                     00165913
              DISPLAY 'REGISTRO ATUALIZADO: ' WRK-ID                    00166013
             WHEN 100                                                   00166113
              DISPLAY 'REGISTRO NAO ENCONTRADO: ' DB2-ID                00166213
             WHEN OTHER                                                 00166313
              MOVE SQLCODE TO WRK-SQLCODE                               00166413
               DISPLAY 'ERRO NA LEITURA ' WRK-SQLCODE                   00166513
             END-EVALUATE.                                              00166613
                                                                        00166713
                                                                        00166813
      *----------------------------------------------------             00166913
       2000-99-FIM.                                EXIT.                00167013
      *----------------------------------------------------             00167113
      *****************************************************             00167213
      *               F I N A L I Z A R                   *             00167313
      *****************************************************             00167413
      *----------------------------------------------------             00167513
       3000-FINALIZAR                             SECTION.              00167613
      *----------------------------------------------------             00167713
                                                                        00167813
            DISPLAY ' '.                                                00167913
            DISPLAY '-FIM DO PROGRAMA-'.                                00168013
                                                                        00168113
      *----------------------------------------------------             00168213
       3000-99-FIM.                                  EXIT.              00168313
      *----------------------------------------------------             00169013
