      *====================================================             00010000
       IDENTIFICATION                            DIVISION.              00020000
      *====================================================             00030000
       PROGRAM-ID. FR16JOIN.                                            00040013
                                                                        00061009
      *====================================================*            00062009
      *   AUTOR....:LUCIANO GUIMARAES                      *            00063009
      *   ANALISTA.:IVAN SANCHES                           *            00063110
      *   DATA ....:14/06/2022                             *            00063209
      *----------------------------------------------------*            00063309
      *   OBJETIVO:LER AS TABELAS FUNC E BENEF E FAZER     *            00063410
      *          A RELACAO DOS PLANOS E GRAVAR EM ARQUIVO  *            00063510
      *                                                    *            00063810
      *----------------------------------------------------*            00064010
      *   BASE DE DADOS:                                   *            00066010
      *   TABELA.DB2..                                     *            00067010
      *    ------              I/O        INCLUDE/BOOK     *            00068010
      *   IVAN.FUNC             I         #BKFUNC----      *            00069010
      *   IVAN.BENEF            I         #BKBENEF---      *            00069110
      *----------------------------------------------------*            00069210
      *   ARQUIVOS:                                        *            00069310
      *    DDNAME              I/O         COPY/BOOK       *            00069410
      *   SRELBENE              O         -----------      *            00070010
      *====================================================*            00080009
       ENVIRONMENT                               DIVISION.              00100010
      *====================================================             00112010
       CONFIGURATION                             SECTION.               00113010
      *----------------------------------------------------             00114010
       SPECIAL-NAMES.                                                   00115010
           DECIMAL-POINT IS COMMA.                                      00116010
                                                                        00117010
       INPUT-OUTPUT                              SECTION.               00118010
       FILE-CONTROL.                                                    00119010
            SELECT SRELBENE ASSIGN TO SRELBENE                          00119110
                FILE STATUS IS WRK-FS-SRELBENE.                         00119210
                                                                        00119310
      *====================================================             00119410
       DATA                                      DIVISION.              00119510
      *====================================================             00119610
       FILE                                      SECTION.               00119710
       FD SRELBENE                                                      00119810
           RECORDING MODE IS F                                          00120010
           LABEL RECORD IS STANDARD                                     00130010
           BLOCK CONTAINS 0 RECORDS.                                    00131010
                                                                        00132010
       01 FD-SRELBENE         PIC X(68).                                00133010
                                                                        00134000
      *====================================================             00137000
       WORKING-STORAGE                           SECTION.               00138001
      *----------------------------------------------------             00138101
           EXEC SQL                                                     00138610
              INCLUDE #BKFUNC                                           00138710
           END-EXEC.                                                    00138810
                                                                        00138910
           EXEC SQL                                                     00139010
              INCLUDE #BKBENEF                                          00139110
           END-EXEC.                                                    00139210
                                                                        00139310
           EXEC SQL                                                     00139410
              INCLUDE SQLCA                                             00139510
           END-EXEC.                                                    00139610
                                                                        00139710
           EXEC SQL                                                     00139810
              DECLARE CFUNC CURSOR FOR                                  00139910
               SELECT ID,NOME,SALARIO,PLANMED, PLANDENT, COTAS          00140010
                FROM IVAN.FUNC F , IVAN.BENEF B                         00140110
                 WHERE F.ID = B.IDFUNC                                  00140210
           END-EXEC.                                                    00140304
                                                                        00140401
      *----------------------------------------------------             00140510
       01 FILLER              PIC X(70) VALUE                           00140610
              '---------VARIAVEIS DE DADOS ----------'.                 00140712
      *----------------------------------------------------             00140810
      *-LRECL: 68-                                                      00141710
                                                                        00141810
       01 WRK-DADOS.                                                    00141910
          05 WRK-ID           PIC 9(05).                                00142010
          05 WRK-NOME         PIC X(30).                                00142110
          05 WRK-SALARIO      PIC 9(10).                                00142210
          05 WRK-PLANMED      PIC X(10).                                00142310
          05 WRK-PLANDENT     PIC X(10).                                00142410
          05 WRK-COTAS        PIC 9(03).                                00142510
      *----------------------------------------------------             00143010
       01 FILLER              PIC X(70) VALUE                           00143110
              '---------VARIAVEIS ACUMULADORAS-------'.                 00143212
      *----------------------------------------------------             00143310
       77 WRK-REG-LIDOS       PIC 9(03).                                00143410
       77 WRK-REG-GRAV        PIC 9(03).                                00143512
      *----------------------------------------------------             00143610
       01 FILLER              PIC X(70) VALUE                           00143710
              '---------VARIAVEIS DE FILE STATUS-----'.                 00143812
      *----------------------------------------------------             00143910
       77 WRK-FS-SRELBENE     PIC 9(02).                                00144010
       77 WRK-SQLCODE         PIC -999.                                 00145010
      *====================================================             00153000
       PROCEDURE                                 DIVISION.              00154000
      *====================================================             00155000
      *****************************************************             00155100
      *          R O T I N A   P R I N C I P A L          *             00155200
      *****************************************************             00155300
      *----------------------------------------------------             00156000
       0000-PRINCIPAL                            SECTION.               00157000
      *----------------------------------------------------             00157100
                                                                        00157200
            PERFORM 1000-INICIALIZAR.                                   00157302
            PERFORM 2000-PROCESSAR UNTIL SQLCODE EQUAL 100.             00157507
            PERFORM 3000-FINALIZAR.                                     00157807
            STOP RUN.                                                   00157907
                                                                        00158000
      *----------------------------------------------------             00158100
       0000-99-FIM.                               EXIT.                 00158200
      *----------------------------------------------------             00158300
      *****************************************************             00158400
      *             I N I C I A L I Z A R                 *             00158500
      *****************************************************             00158600
      *----------------------------------------------------             00158700
       1000-INICIALIZAR                           SECTION.              00158800
      *----------------------------------------------------             00158900
                                                                        00159000
            EXEC SQL                                                    00159104
               OPEN CFUNC                                               00159204
            END-EXEC.                                                   00159304
                                                                        00159410
            EVALUATE SQLCODE                                            00159510
             WHEN 0                                                     00159610
                PERFORM 1100-LER-FUNCIONARIO                            00159710
             WHEN 100                                                   00159810
                DISPLAY 'SEM FUNCIONARIOS'                              00159904
             WHEN OTHER                                                 00160010
                MOVE SQLCODE TO WRK-SQLCODE                             00160104
                DISPLAY 'ERRO ' WRK-SQLCODE ' NO OPEN '                 00160210
                STOP RUN                                                00160304
            END-EVALUATE.                                               00160410
                                                                        00160510
            OPEN OUTPUT SRELBENE.                                       00160610
            PERFORM 1200-TESTAR-STATUS.                                 00160711
      *----------------------------------------------------             00160800
       1000-99-FIM.                                 EXIT.               00160900
      *----------------------------------------------------             00161000
      *----------------------------------------------------             00161104
       1100-LER-FUNCIONARIO                       SECTION.              00161210
      *----------------------------------------------------             00161304
                                                                        00161404
            EXEC SQL                                                    00161504
             FETCH CFUNC                                                00161604
              INTO :DB2-ID,                                             00161704
                   :DB2-NOME,                                           00161804
                   :DB2-SALARIO,                                        00161912
                   :DB2-PLANMED,                                        00162012
                   :DB2-PLANDENT,                                       00162112
                   :DB2-COTAS                                           00162212
            END-EXEC.                                                   00162304
                                                                        00162404
            EVALUATE SQLCODE                                            00162504
             WHEN 0                                                     00162604
                ADD 1 TO WRK-REG-LIDOS                                  00162712
                CONTINUE                                                00162805
             WHEN 100                                                   00163404
                DISPLAY ' '                                             00163504
                DISPLAY 'FIM DE TABELA ' DB2-ID                         00163606
             WHEN OTHER                                                 00163704
                MOVE SQLCODE TO  WRK-SQLCODE                            00163804
                DISPLAY ' '                                             00163904
                DISPLAY 'ERRO NA LEITURA' WRK-SQLCODE                   00164004
            END-EVALUATE.                                               00164104
      *----------------------------------------------------             00164204
       1100-99-FIM.                                  EXIT.              00164310
      *----------------------------------------------------             00164404
      *----------------------------------------------------             00164811
       1200-TESTAR-STATUS                         SECTION.              00164911
      *----------------------------------------------------             00165011
            IF WRK-FS-SRELBENE NOT EQUAL 0                              00165111
               DISPLAY 'ERRO NO OPEN '                                  00165212
                  STOP RUN                                              00165311
            END-IF.                                                     00165411
      *----------------------------------------------------             00165511
       1200-99-FIM.                                  EXIT.              00165611
      *----------------------------------------------------             00165711
      *****************************************************             00165811
      *               P R O C E S S A R                   *             00165911
      *****************************************************             00166011
      *----------------------------------------------------             00166111
       2000-PROCESSAR                             SECTION.              00166211
      *----------------------------------------------------             00166311
                                                                        00167011
            PERFORM 2500-MOVE-DADOS.                                    00167512
                                                                        00167612
            WRITE FD-SRELBENE FROM WRK-DADOS.                           00167712
            ADD 1 TO WRK-REG-GRAV.                                      00167812
                                                                        00167912
            PERFORM 1100-LER-FUNCIONARIO.                               00168012
                                                                        00168106
      *----------------------------------------------------             00168706
       2000-99-FIM.                                EXIT.                00168806
      *----------------------------------------------------             00168906
      *----------------------------------------------------             00169006
       2500-MOVE-DADOS                            SECTION.              00169112
      *----------------------------------------------------             00169206
            INITIALIZE WRK-DADOS.                                       00169312
            MOVE DB2-ID       TO WRK-ID.                                00169412
            MOVE DB2-NOME     TO WRK-NOME.                              00169512
            MOVE DB2-SALARIO  TO WRK-SALARIO.                           00169612
            MOVE DB2-PLANMED  TO WRK-PLANMED.                           00169712
            MOVE DB2-PLANDENT TO WRK-PLANDENT.                          00169812
            MOVE DB2-COTAS    TO WRK-COTAS.                             00169912
      *----------------------------------------------------             00170706
       2500-99-FIM.                                EXIT.                00170812
      *----------------------------------------------------             00170906
      *****************************************************             00172106
      *               F I N A L I Z A R                   *             00172206
      *****************************************************             00172306
      *----------------------------------------------------             00172406
       3000-FINALIZAR                             SECTION.              00172506
      *----------------------------------------------------             00172606
                                                                        00175006
            EXEC SQL                                                    00180004
              CLOSE CFUNC                                               00190004
            END-EXEC.                                                   00200004
            CLOSE SRELBENE.                                             00201012
                                                                        00202012
            DISPLAY 'REGISTROS LIDOS   : ' WRK-REG-LIDOS.               00203012
            DISPLAY 'REGISTROS GRAVADOS: ' WRK-REG-GRAV.                00204012
      *----------------------------------------------------             00210004
       3000-99-FIM.                                  EXIT.              00220004
      *----------------------------------------------------             00230004
