      *=============================================================*   00010000
       IDENTIFICATION                            DIVISION.              00020000
      *=============================================================*   00030000
                                                                        00040000
       PROGRAM-ID. FR16EX01.                                            00050000
      *=============================================================*   00060000
      *             T R E I N A M E N T O                           *   00061000
      *=============================================================*   00062000
      * PROGRAMA......: FR16EX01.                                   *   00062100
      *-------------------------------------------------------------*   00062200
      * PROGRAMADOR...: LUCIANO GUIMARAES.                          *   00062300
      * DATA..........: 26/05/2022                                  *   00062400
      * EMPRESA.......: FOURSYS                                     *   00062500
      * INSTRUTOR.....: IVAN SANCHES                                *   00062600
      *-------------------------------------------------------------*   00062700
      * OBJETIVO......: LER O ARQUIVO CONTENDO INFORMACOES DOS FUNC *   00062800
      *                 IONARIOS E GRAVAR O RELATORIO EM UM ARQUIVO.*   00062900
      *-------------------------------------------------------------*   00063000
      *   ARQUIVOS...:                                              *   00065000
      *    DDNAME              I/O                                  *   00066000
      *    FUNC2                I                                   *   00067003
      *    PROJ                 I                                   *   00067103
      *    FUNPROJ              O                                   *   00068003
      *-------------------------------------------------------------*   00070000
      *   MODULOS....:                             INCLUDE/BOOK     *   00080000
      *   GRAVALOG -   TRATAMENTO DE ERROS            GLOG2         *   00090000
      *=============================================================*   00100000
                                                                        00120000
      *=============================================================*   00130000
       ENVIRONMENT                               DIVISION.              00140000
      *=============================================================*   00150000
      *-------------------------------------------------------------*   00160000
       CONFIGURATION                             SECTION.               00170000
      *-------------------------------------------------------------*   00180000
       SPECIAL-NAMES.                                                   00190000
           DECIMAL-POINT IS COMMA.                                      00200000
                                                                        00210000
       INPUT-OUTPUT                              SECTION.               00220000
       FILE-CONTROL.                                                    00230000
            SELECT FUNC   ASSIGN TO FUNC                                00240000
                FILE STATUS  IS WRK-FS-FUNC.                            00250000
                                                                        00251000
            SELECT RELAT     ASSIGN TO RELAT                            00260000
                FILE STATUS  IS WRK-FS-RELAT.                           00261000
      *=============================================================*   00270000
       DATA                                      DIVISION.              00280000
      *=============================================================*   00290000
       FILE                                      SECTION.               00300000
      *-------------------------------------------------------------*   00300100
                                                                        00301000
       FD FUNC                                                          00310000
           RECORDING MODE IS F                                          00320000
           BLOCK CONTAINS 0 RECORDS.                                    00340000
                                                                        00350000
       01 FD-FUNC.                                                      00351000
          05 FD-IDFUNC      PIC 9(05).                                  00352000
          05 FD-NOMEFUNC    PIC X(30).                                  00353000
          05 FD-SETORFUNC   PIC X(04).                                  00354000
          05 FD-SALFUNC     PIC 9(06).                                  00355000
                                                                        00355100
                                                                        00355200
       FD RELAT                                                         00355300
           RECORDING  MODE IS F                                         00355400
           BLOCK CONTAINS 0 RECORDS.                                    00355500
                                                                        00355600
       01 FD-RELAT  PIC X(57).                                          00355701
      *-------------------------------------------------------------*   00480000
       WORKING-STORAGE                           SECTION.               00490000
      *-------------------------------------------------------------*   00500000
                                                                        00501000
      *-------------------------------------------------------------*   00501100
       01 FILLER PIC X(45) VALUE                                        00501200
              '------------------#GLOG----------------------'.          00501300
      *-------------------------------------------------------------*   00501400
                                                                        00501500
       COPY '#GLOG'.                                                    00502000
                                                                        00503000
      *-------------------------------------------------------------*   00503100
       01 FILLER PIC X(45) VALUE                                        00503200
              '-------------VARIAVEL DE CABECALHO-----------'.          00503300
      *-------------------------------------------------------------*   00503400
      *-LRECL:65 E 58-                                                  00503500
       01 WKR-CABECALHO.                                                00504000
          05 WRK-CABECALHO-1.                                           00505000
            10 FILLER       PIC X(12).                                  00506000
            10 FILLER       PIC X(44) VALUE                             00506200
               'RELATORIO DE FUNCIONARIOS              PAG: '.          00506300
            10 WRK-PAG      PIC 9(02) VALUE 1.                          00506600
            10 FILLER       PIC X(07).                                  00506900
          05 WRK-CABECALHO-2.                                           00507000
            10 FILLER       PIC X(09) VALUE 'ID       '.                00507200
            10 FILLER       PIC X(04) VALUE 'NOME'.                     00508000
            10 FILLER       PIC X(26).                                  00509000
            10 FILLER       PIC X(12) VALUE 'SETOR       '.             00509100
            10 FILLER       PIC X(07) VALUE 'SALARIO'.                  00509200
                                                                        00509900
      *-------------------------------------------------------------*   00510000
       01 FILLER PIC X(45) VALUE                                        00510100
              '--------VARIAVEIS DE ARMAZENAMENTO-----------'.          00510200
      *-------------------------------------------------------------*   00510300
      *-LRECL:56-                                                       00510400
       01 WRK-FUNCIO.                                                   00510500
          05 WRK-IDFUNC     PIC 9(05).                                  00511000
          05 FILLER         PIC X(04).                                  00511100
          05 WRK-NOMEFUNC   PIC X(30).                                  00512000
          05 WRK-SETORFUNC  PIC X(04).                                  00513000
          05 FILLER         PIC X(09).                                  00513100
          05 WRK-SALFUNC    PIC 9(06).                                  00514000
          05 FILLER         PIC X(01).                                  00515000
                                                                        00515100
      *-LRECL:24-                                                       00515200
       01 WRK-SALARIO.                                                  00515300
          05 FILLER          PIC X(19) VALUE '   TOTAL DO SETOR: '.     00515400
          05 FILLER          PIC X(32) VALUE SPACES.                    00515500
          05 WRK-SAL-ACUM    PIC 9(07).                                 00515600
                                                                        00515700
       01 WRK-SALARIO-TOTAL.                                            00515800
          05 FILLER          PIC X(16) VALUE '   TOTAL GERAL: '.        00515900
          05 FILLER          PIC X(35) VALUE SPACES.                    00516000
          05 WRK-SAL-TOTAL   PIC 9(07).                                 00516100
                                                                        00516500
      *-------------------------------------------------------------*   00516600
       01 FILLER PIC X(45) VALUE                                        00516700
              '-----------VARIAVEIS DE FILE STATUS----------'.          00516800
      *-------------------------------------------------------------*   00516900
       77 WRK-FS-FUNC       PIC 9(02).                                  00520000
       77 WRK-FS-RELAT      PIC 9(02).                                  00521000
                                                                        00521100
      *-------------------------------------------------------------*   00521200
       01 FILLER PIC X(45) VALUE                                        00521300
              '------VARIAVEIS ACUMULADORAS E DE SOMA-------'.          00521400
      *-------------------------------------------------------------*   00521500
                                                                        00521600
       77 WRK-ERRORLOG      PIC X(08) VALUE 'ERRORLOG'.                 00521700
       77 WRK-LINHAS        PIC 9(03) VALUE 1.                          00522000
       77 WRK-LINHAS-BRANCO PIC X(58) VALUE SPACES.                     00522100
       77 WRK-REG-LIDOS     PIC 9(02).                                  00522300
       77 WRK-ESCOLHA-LINHAS PIC 9(03).                                 00522500
       77 WRK-SETOR-ATUAL PIC X(04).                                    00522600
      *=============================================================*   00540000
       PROCEDURE                                 DIVISION.              00550000
      *=============================================================*   00560000
      ***************************************************************   00570000
      *          R O T I N A   P R I N C I P A L                    *   00571000
      ***************************************************************   00572000
      *--------------------------------------------------------------   00573000
       0000-PRINCIPAL                            SECTION.               00574000
      *--------------------------------------------------------------   00575000
             PERFORM 1000-INICIALIZAR.                                  00580000
             PERFORM 2000-PROCESSAR UNTIL WRK-FS-FUNC EQUAL 10.         00590000
             PERFORM 3000-FINALIZAR.                                    00600000
             STOP RUN.                                                  00610000
      *--------------------------------------------------------------   00611000
       0000-99-FIM.                              EXIT.                  00612000
      *--------------------------------------------------------------   00613000
      ***************************************************************   00614000
      *             I N I C I A L I Z A R                           *   00615000
      ***************************************************************   00616000
      *--------------------------------------------------------------   00617000
       1000-INICIALIZAR                          SECTION.               00618000
      *--------------------------------------------------------------   00619000
             ACCEPT WRK-ESCOLHA-LINHAS.                                 00620000
             OPEN INPUT FUNC                                            00630000
                 OUTPUT RELAT.                                          00640000
                  PERFORM 5100-TESTAR-STATUS.                           00640100
                   READ FUNC.                                           00640300
                    MOVE FD-SETORFUNC TO WRK-SETOR-ATUAL.               00640400
                    PERFORM 1050-IMPRIME-CABECALHO.                     00640500
                                                                        00640600
             IF WRK-FS-FUNC EQUAL 10                                    00642100
               DISPLAY 'FIM DE ARQUIVO'                                 00642200
             END-IF.                                                    00642300
      *--------------------------------------------------------------   00671000
       1000-99-FIM.                              EXIT.                  00672000
      *--------------------------------------------------------------   00673000
      *--------------------------------------------------------------   00674000
       1050-IMPRIME-CABECALHO                    SECTION.               00675000
      *--------------------------------------------------------------   00675100
             WRITE FD-RELAT FROM WRK-LINHAS-BRANCO.                     00675200
             WRITE FD-RELAT FROM WRK-CABECALHO-1 AFTER PAGE.            00675300
             WRITE FD-RELAT FROM WRK-LINHAS-BRANCO.                     00675400
             WRITE FD-RELAT FROM WRK-CABECALHO-2.                       00675500
               ADD 4 TO WRK-LINHAS.                                     00675600
      *--------------------------------------------------------------   00676600
       1050-99-FIM.                              EXIT.                  00676700
      *--------------------------------------------------------------   00676800
      ***************************************************************   00676900
      *               P R O C E S S A R                             *   00677000
      ***************************************************************   00677100
      *--------------------------------------------------------------   00677200
       2000-PROCESSAR                            SECTION.               00678000
      *--------------------------------------------------------------   00679000
             ADD 1 TO WRK-REG-LIDOS.                                    00680000
             IF FD-SETORFUNC EQUAL WRK-SETOR-ATUAL                      00690000
               ADD FD-SALFUNC TO WRK-SAL-ACUM                           00691000
               ADD FD-SALFUNC TO WRK-SAL-TOTAL                          00692000
                                                                        00693200
             ELSE                                                       00693300
              WRITE FD-RELAT FROM WRK-SALARIO                           00693500
               MOVE 0 TO  WRK-SAL-ACUM                                  00693600
                MOVE FD-SETORFUNC TO WRK-SETOR-ATUAL                    00693700
                 ADD FD-SALFUNC TO WRK-SAL-ACUM                         00693800
                  ADD FD-SALFUNC TO WRK-SAL-TOTAL                       00693900
             END-IF.                                                    00694000
                                                                        00694100
             MOVE FD-IDFUNC    TO WRK-IDFUNC.                           00694200
             MOVE FD-NOMEFUNC  TO WRK-NOMEFUNC.                         00694300
             MOVE FD-SETORFUNC TO WRK-SETORFUNC.                        00694400
             MOVE FD-SALFUNC   TO WRK-SALFUNC.                          00694500
             WRITE FD-RELAT FROM  WRK-FUNCIO                            00694600
                                                                        00694700
             ADD 1 TO WRK-LINHAS                                        00694800
             IF WRK-LINHAS GREATER 30                                   00694900
              ADD 1 TO WRK-PAG                                          00695000
               MOVE 1 TO WRK-LINHAS                                     00695100
                PERFORM 1050-IMPRIME-CABECALHO                          00695200
             END-IF                                                     00695300
                                                                        00695500
                                                                        00698900
             READ FUNC.                                                 00699200
             IF WRK-FS-FUNC EQUAL 10                                    00699300
               WRITE   FD-RELAT FROM WRK-SALARIO                        00699500
               WRITE   FD-RELAT FROM WRK-SALARIO-TOTAL                  00699700
             END-IF.                                                    00699800
                                                                        00699900
      *-------------------------------------------------------------    00701100
       2000-99-FIM.                              EXIT.                  00702000
      *--------------------------------------------------------------   00703000
      ***************************************************************   00704000
      *               F I N A L I Z A R                             *   00705000
      ***************************************************************   00706000
      *--------------------------------------------------------------   00707000
       3000-FINALIZAR                            SECTION.               00708000
      *--------------------------------------------------------------   00709000
            CLOSE FUNC RELAT.                                           00710100
            DISPLAY ' '.                                                00710200
            DISPLAY '--------------------'                              00710300
            DISPLAY 'FIM DO PROCESSAMENTO'.                             00710500
            DISPLAY '--------------------'                              00710600
            DISPLAY ' '.                                                00710700
            DISPLAY 'TOTAL DE REGISTROS LIDOS: ' WRK-REG-LIDOS.         00722000
      *--------------------------------------------------------------   00731000
       3000-99-FIM.                              EXIT.                  00732000
      *--------------------------------------------------------------   00733000
      *--------------------------------------------------------------   00830000
       5100-TESTAR-STATUS                        SECTION.               00840000
      *--------------------------------------------------------------   00850000
            IF WRK-FS-FUNC NOT EQUAL 0                                  00860000
               MOVE 'FR16EX01'             TO WRK-PROGRAMA              00861002
               MOVE '5100'                 TO WRK-SECAO                 00861100
               MOVE 'ERRO NO OPEN FUNC'    TO WRK-MENSAGEM              00861200
               MOVE WRK-FS-FUNC            TO WRK-STATUS                00861300
                 PERFORM 6000-TRATA-ERROS                               00862000
            END-IF.                                                     00870000
      *--------------------------------------------------------------   00880000
       5100-99-FIM.                              EXIT.                  00890000
      *--------------------------------------------------------------   00900000
      *--------------------------------------------------------------   00990000
       6000-TRATA-ERROS                          SECTION.               01000000
      *--------------------------------------------------------------   01010000
            CALL WRK-ERRORLOG USING WRK-DADOS.                          01011100
            GOBACK.                                                     01012000
      *--------------------------------------------------------------   01020000
       6000-99-FIM.                              EXIT.                  01030000
      *--------------------------------------------------------------   01040000
