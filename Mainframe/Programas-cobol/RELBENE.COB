      *====================================================             00010000
       IDENTIFICATION                            DIVISION.              00020000
      *====================================================             00030000
       PROGRAM-ID. RELBENE.                                             00040013
                                                                        00061009
      *====================================================*            00062009
      *   AUTOR....:LUCIANO GUIMARAES                      *            00063009
      *   ANALISTA.:IVAN SANCHES                           *            00063110
      *   DATA ....:14/06/2022                             *            00063209
      *----------------------------------------------------*            00063309
      *   OBJETIVO: LER O ARQUIVO COM A RELACAO DE PLANOS  *            00063413
      *            DE SAUDE E GERAR UM RELATORIO.          *            00063513
      *                                                    *            00063810
      *----------------------------------------------------*            00064010
      *   ARQUIVOS...:                                     *            00066013
      *    DDNAME              I/O                         *            00067013
      *    SRELBENE             I                          *            00068013
      *----------------------------------------------------*            00069210
      *====================================================*            00080009
       ENVIRONMENT                               DIVISION.              00100010
      *====================================================             00112010
       CONFIGURATION                             SECTION.               00113010
      *----------------------------------------------------             00114010
       SPECIAL-NAMES.                                                   00115010
           DECIMAL-POINT IS COMMA.                                      00116010
                                                                        00117010
       INPUT-OUTPUT                              SECTION.               00118010
       FILE-CONTROL.                                                    00119010
            SELECT SRELBENE ASSIGN TO SRELBENE                          00119110
                FILE STATUS IS WRK-FS-SRELBENE.                         00119210
                                                                        00119313
            SELECT RELABENE ASSIGN TO RELABENE                          00119413
                FILE STATUS  IS WRK-FS-RELABENE.                        00119513
                                                                        00119610
      *====================================================             00119710
       DATA                                      DIVISION.              00119810
      *====================================================             00119910
       FILE                                      SECTION.               00120010
      *----------------------------------------------------             00120113
       FD SRELBENE                                                      00120313
           RECORDING  MODE IS F                                         00120413
           BLOCK CONTAINS 0 RECORDS.                                    00120513
                                                                        00120613
      *-LRECL :68-                                                      00120713
       01 FD-SRELBENE.                                                  00120813
          05 FD-ID          PIC 9(05).                                  00120913
          05 FD-NOME        PIC X(30).                                  00121013
          05 FD-SALARIO     PIC 9(10).                                  00121113
          05 FD-PLANMED     PIC X(10).                                  00121213
          05 FD-PLANDENT    PIC X(10).                                  00121313
          05 FD-COTAS       PIC 9(03).                                  00121413
                                                                        00121513
       FD RELABENE                                                      00121613
           RECORDING  MODE IS F                                         00121713
           BLOCK CONTAINS 0 RECORDS.                                    00121813
                                                                        00121913
       01 FD-RELABENE       PIC X(90).                                  00122013
                                                                        00134000
      *====================================================             00137000
       WORKING-STORAGE                           SECTION.               00138001
      *----------------------------------------------------             00138101
                                                                        00138213
      *----------------------------------------------------             00140510
       01 FILLER              PIC X(70) VALUE                           00140610
              '---------VARIAVEIS DE DADOS ----------'.                 00140712
      *----------------------------------------------------             00140810
      *-LRECL: 89-                                                      00141713
                                                                        00141810
       01 WRK-SRELBENE.                                                 00141913
          05 WRK-ID           PIC 9(05).                                00142013
          05 FILLER           PIC X(04).                                00142113
          05 WRK-NOME         PIC X(30).                                00142213
          05 FILLER           PIC X(01).                                00142313
          05 WRK-SALARIO      PIC 9(10).                                00142413
          05 FILLER           PIC X(04).                                00142513
          05 WRK-PLANMED      PIC X(10).                                00142613
          05 FILLER           PIC X(04).                                00142713
          05 WRK-PLANDENT     PIC X(10).                                00142813
          05 FILLER           PIC X(04).                                00142913
          05 WRK-COTAS        PIC 9(03).                                00143013
          05 FILLER           PIC X(04).                                00143113
      *----------------------------------------------------             00143210
       01 FILLER              PIC X(70) VALUE                           00143310
              '---------VARIAVEIS DE EDICAO----------'.                 00143413
      *----------------------------------------------------             00143513
       01 WRK-CABECALHOS.                                               00143614
          05 WRK-CABECALHO-1.                                           00143713
            10 FILLER         PIC X(13).                                00143813
            10 FILLER         PIC X(37) VALUE                           00143913
               'RELATORIO DE FUNCIONARIOS       PAG: '.                 00144013
            10 WRK-PAG        PIC 9(02) VALUE 1.                        00144113
            10 FILLER         PIC X(37).                                00144213
          05 WRK-CABECALHO-2.                                           00144513
            10 FILLER         PIC X(09) VALUE 'ID       '.              00144613
            10 FILLER         PIC X(04) VALUE 'NOME'.                   00144713
            10 FILLER         PIC X(27).                                00144813
            10 FILLER         PIC X(14) VALUE 'SALARIO      '.          00144913
            10 FILLER         PIC X(14) VALUE 'PLANO MEDICO '.          00145013
            10 FILLER         PIC X(14) VALUE 'PLANO DENTAL '.          00145113
            10 FILLER         PIC X(07) VALUE 'COTAS  '.                00145213
       77 WRK-FIM-REL         PIC X(16) VALUE                           00145313
                                       'FIM DE RELATORIO'.              00145414
       77 WRK-MSG-REG-LIDOS   PIC X(26) VALUE                           00145514
                                       'TOTAL DE REGISTROS LIDOS: '.    00145613
       77 WRK-MSG-SAL-ACUM    PIC X(26) VALUE                           00145714
                                       'SALARIO TOTAL ACUMULADO : '.    00145813
       77 WRK-MSG-FIM-REL     PIC X(50) VALUE                           00145913
                    '---------------FINAL DO RELATORIO------------'.    00146013
      *----------------------------------------------------             00148013
       01 FILLER              PIC X(70) VALUE                           00149013
              '---------VARIAVEIS ACUMULADORAS-----'.                   00150013
      *----------------------------------------------------             00151013
       77 WRK-ACUM-LIDOS      PIC 9(03).                                00151113
       77 WRK-ACUM-SAL        PIC 9(08)V99.                             00151213
      *----------------------------------------------------             00152013
       01 FILLER              PIC X(70) VALUE                           00152113
              '---------VARIAVEIS DE APOIO---------'.                   00152213
      *----------------------------------------------------             00152313
       77 WRK-ESPACO          PIC X(01) VALUE SPACES.                   00152413
       77 WRK-LINHA           PIC 9(02) VALUE 1.                        00152513
      *----------------------------------------------------             00152613
       01 FILLER              PIC X(70) VALUE                           00152713
              '---------VARIAVEIS MASCARA----------'.                   00152813
      *----------------------------------------------------             00152913
       77 WRK-SAL-ACUM-ED     PIC Z.ZZZ.ZZ9,99.                         00153013
      *----------------------------------------------------             00153113
       01 FILLER              PIC X(70) VALUE                           00153213
              '---------VARIAVEIS DE FILE STATUS-----'.                 00153313
      *----------------------------------------------------             00153413
       77 WRK-FS-SRELBENE     PIC 9(02).                                00153513
       77 WRK-FS-RELABENE     PIC 9(02).                                00153613
      *====================================================             00153713
       PROCEDURE                                 DIVISION.              00154000
      *====================================================             00155000
      *****************************************************             00155100
      *          R O T I N A   P R I N C I P A L          *             00155200
      *****************************************************             00155300
      *----------------------------------------------------             00156000
       0000-PRINCIPAL                            SECTION.               00157000
      *----------------------------------------------------             00157100
                                                                        00157200
            PERFORM 1000-INICIALIZAR.                                   00157314
            PERFORM 2000-PROCESSAR UNTIL WRK-FS-SRELBENE EQUAL 10.      00157513
            PERFORM 3000-FINALIZAR.                                     00157807
            STOP RUN.                                                   00157907
                                                                        00158000
      *----------------------------------------------------             00158100
       0000-99-FIM.                               EXIT.                 00158200
      *----------------------------------------------------             00158300
      *****************************************************             00158400
      *             I N I C I A L I Z A R                 *             00158500
      *****************************************************             00158600
      *----------------------------------------------------             00158700
       1000-INICIALIZAR                           SECTION.              00158800
      *----------------------------------------------------             00158900
                                                                        00159000
            OPEN INPUT SRELBENE                                         00159113
                 OUTPUT RELABENE.                                       00159213
            PERFORM 1200-TESTAR-STATUS.                                 00159313
            READ SRELBENE.                                              00159413
            PERFORM 1100-IMPRIME-CABECALHO.                             00159513
                                                                        00159613
      *----------------------------------------------------             00160800
       1000-99-FIM.                                 EXIT.               00160900
      *----------------------------------------------------             00161000
      *----------------------------------------------------             00161104
       1100-IMPRIME-CABECALHO                    SECTION.               00161213
      *----------------------------------------------------             00161304
                                                                        00161404
            WRITE FD-RELABENE FROM WRK-ESPACO.                          00161513
                                                                        00161613
            IF WRK-PAG EQUAL 1                                          00161713
              WRITE FD-RELABENE FROM WRK-CABECALHO-1                    00161813
            ELSE                                                        00161913
              WRITE FD-RELABENE FROM WRK-CABECALHO-1 AFTER PAGE         00162013
            END-IF.                                                     00162113
                                                                        00162213
            WRITE FD-RELABENE FROM WRK-ESPACO.                          00162313
            WRITE FD-RELABENE FROM WRK-CABECALHO-2.                     00162413
            ADD 4 TO WRK-LINHA.                                         00162513
                                                                        00162613
      *----------------------------------------------------             00164204
       1100-99-FIM.                                  EXIT.              00164310
      *----------------------------------------------------             00164404
      *----------------------------------------------------             00164811
       1200-TESTAR-STATUS                         SECTION.              00164911
      *----------------------------------------------------             00165011
            IF WRK-FS-SRELBENE NOT EQUAL 0                              00165111
               DISPLAY 'ERRO NO OPEN '                                  00165212
                  STOP RUN                                              00165311
            END-IF.                                                     00165411
      *----------------------------------------------------             00165511
       1200-99-FIM.                                  EXIT.              00165611
      *----------------------------------------------------             00165711
      *****************************************************             00165811
      *               P R O C E S S A R                   *             00165911
      *****************************************************             00166011
      *----------------------------------------------------             00166111
       2000-PROCESSAR                             SECTION.              00166211
      *----------------------------------------------------             00166311
            ADD 1 TO WRK-ACUM-LIDOS.                                    00167013
            ADD FD-SALARIO TO WRK-ACUM-SAL.                             00167514
                                                                        00167613
            PERFORM 2500-MOVE-DADOS.                                    00167713
            WRITE FD-RELABENE FROM WRK-SRELBENE.                        00167914
            PERFORM 2600-QUEBRA-LINHA.                                  00168013
                                                                        00168113
            READ SRELBENE.                                              00168213
                                                                        00168313
            IF WRK-FS-SRELBENE NOT EQUAL 0                              00168413
               DISPLAY 'FIM DE ARQUIVO. '                               00168513
            END-IF.                                                     00168613
      *----------------------------------------------------             00168706
       2000-99-FIM.                                EXIT.                00168806
      *----------------------------------------------------             00168906
      *----------------------------------------------------             00169006
       2500-MOVE-DADOS                            SECTION.              00169112
      *----------------------------------------------------             00169206
            MOVE FD-ID       TO WRK-ID.                                 00169513
            MOVE FD-NOME     TO WRK-NOME.                               00169613
            MOVE FD-SALARIO  TO WRK-SALARIO.                            00169713
            MOVE FD-PLANMED  TO WRK-PLANMED.                            00169813
            MOVE FD-PLANDENT TO WRK-PLANDENT.                           00169913
            MOVE FD-COTAS    TO WRK-COTAS.                              00170013
      *----------------------------------------------------             00171206
       2500-99-FIM.                                EXIT.                00171312
      *----------------------------------------------------             00172006
      *----------------------------------------------------             00172113
       2600-QUEBRA-LINHA                          SECTION.              00172213
      *----------------------------------------------------             00172313
            ADD 1 TO WRK-LINHA.                                         00173013
            IF WRK-LINHA GREATER 06                                     00173113
               PERFORM 2800-NOVA-PAGINA                                 00173213
            END-IF.                                                     00173313
      *----------------------------------------------------             00173413
       2600-99-FIM.                                EXIT.                00173513
      *----------------------------------------------------             00173613
      *----------------------------------------------------             00173713
       2800-NOVA-PAGINA                           SECTION.              00173813
      *----------------------------------------------------             00173913
            ADD 1 TO WRK-PAG.                                           00174013
            MOVE 1 TO WRK-LINHA.                                        00174113
            PERFORM 1100-IMPRIME-CABECALHO.                             00174213
      *----------------------------------------------------             00174413
       2800-99-FIM.                                EXIT.                00174513
      *----------------------------------------------------             00174613
      *****************************************************             00174713
      *               F I N A L I Z A R                   *             00174813
      *****************************************************             00174913
      *----------------------------------------------------             00175013
       3000-FINALIZAR                             SECTION.              00175113
      *----------------------------------------------------             00175213
            MOVE WRK-ACUM-SAL TO WRK-SAL-ACUM-ED.                       00175314
            WRITE FD-RELABENE FROM WRK-MSG-FIM-REL.                     00176014
            WRITE FD-RELABENE FROM WRK-ESPACO.                          00177013
            WRITE FD-RELABENE FROM WRK-MSG-REG-LIDOS.                   00178014
            WRITE FD-RELABENE FROM WRK-ACUM-LIDOS.                      00179013
            WRITE FD-RELABENE FROM WRK-MSG-SAL-ACUM.                    00179314
            WRITE FD-RELABENE FROM WRK-SAL-ACUM-ED.                     00179413
            CLOSE SRELBENE RELABENE.                                    00179513
      *----------------------------------------------------             00210004
       3000-99-FIM.                                  EXIT.              00220004
      *----------------------------------------------------             00230004
