      *====================================================             00010000
       IDENTIFICATION                            DIVISION.              00020000
      *====================================================             00030000
       PROGRAM-ID. FR16DB02.                                            00040003
      *====================================================             00061000
      *             T R E I N A M E N T O                 *             00062000
      *====================================================             00063000
      * PROGRAMA......: FR16DB02.                         *             00064003
      *---------------------------------------------------*             00066000
      * PROGRAMADOR...: LUCIANO GUIMARAES.                *             00067000
      * DATA..........: 07/06/2022                        *             00068000
      * EMPRESA.......: FOURSYS                           *             00069000
      * INSTRUTOR.....: IVAN SANCHES                      *             00069100
      *---------------------------------------------------*             00069200
      * OBJETIVO......: ACESSAR A TABELA DB2 FUNC E LER   *             00069302
      *                 TODOS OS REGISTROS                *             00069403
      *                                                   *             00070000
      *----------------------------------------------------             00080000
      *====================================================             00110000
       ENVIRONMENT                               DIVISION.              00120000
      *====================================================             00130000
       CONFIGURATION                             SECTION.               00131000
       SPECIAL-NAMES.                                                   00132000
           DECIMAL-POINT  IS COMMA.                                     00133000
                                                                        00134000
      *====================================================             00135000
       DATA                                      DIVISION.              00136000
      *====================================================             00137000
       WORKING-STORAGE                           SECTION.               00138001
      *----------------------------------------------------             00138101
                                                                        00138201
           EXEC SQL                                                     00139001
              INCLUDE #BKFUNC                                           00139101
           END-EXEC.                                                    00139201
                                                                        00139301
           EXEC SQL                                                     00139401
              INCLUDE SQLCA                                             00139501
           END-EXEC.                                                    00139601
                                                                        00139704
           EXEC SQL                                                     00139804
              DECLARE CFUNC CURSOR FOR                                  00139904
               SELECT * FROM IVAN.FUNC                                  00140004
                ORDER BY NOME                                           00140104
           END-EXEC.                                                    00140204
                                                                        00140301
      *----------------VARIAVEIS DE APOIO------------------             00140404
       77 WRK-SQLCODE                   PIC -999.                       00141002
       77 WRK-NULL-EMAIL                PIC S9(4) COMP.                 00150002
       77 WRK-MEDIA                     PIC 9(08)V99 COMP.              00150106
       77 WRK-MAIOR-VALOR               PIC 9(08)V99 VALUE 1.           00150207
      *----------------VARIAVEIS ACUMULADORAS--------------             00150306
       77 WRK-REG-LIDOS                 PIC 9(02) VALUE ZEROS.          00152006
       77 WRK-SALARIO-TOTAL             PIC 9(08)V99 COMP.              00152207
      *----------------VARIAVEIS MASCARAS------------------             00152306
       77 WRK-SALARIO-ED                PIC ZZZ.ZZZ,ZZ.                 00152407
       77 WRK-MEDIA-ED                  PIC ZZZ.ZZZ,ZZ.                 00152506
       77 WRK-SALARIO-TOT-ED            PIC ZZZ.ZZZ,ZZ.                 00152607
       77 WRK-MAIOR-VALOR-ED            PIC ZZZ.ZZZ,ZZ.                 00152707
      *====================================================             00153000
       PROCEDURE                                 DIVISION.              00154000
      *====================================================             00155000
      *****************************************************             00155100
      *          R O T I N A   P R I N C I P A L          *             00155200
      *****************************************************             00155300
      *----------------------------------------------------             00156000
       0000-PRINCIPAL                            SECTION.               00157000
      *----------------------------------------------------             00157100
                                                                        00157200
            PERFORM 1000-INICIALIZAR.                                   00157302
            PERFORM 2000-PROCESSAR UNTIL SQLCODE EQUAL 100.             00157507
            PERFORM 3000-FINALIZAR.                                     00157807
            STOP RUN.                                                   00157907
                                                                        00158000
      *----------------------------------------------------             00158100
       0000-99-FIM.                               EXIT.                 00158200
      *----------------------------------------------------             00158300
      *****************************************************             00158400
      *             I N I C I A L I Z A R                 *             00158500
      *****************************************************             00158600
      *----------------------------------------------------             00158700
       1000-INICIALIZAR                           SECTION.              00158800
      *----------------------------------------------------             00158900
                                                                        00159000
            EXEC SQL                                                    00159104
               OPEN CFUNC                                               00159204
            END-EXEC.                                                   00159304
             EVALUATE SQLCODE                                           00159404
              WHEN 0                                                    00159504
                PERFORM 4000-LER-FUNCIONARIO                            00159604
                MOVE DB2-SALARIO TO WRK-MAIOR-VALOR                     00159707
              WHEN 100                                                  00159804
                DISPLAY 'SEM FUNCIONARIOS'                              00159904
              WHEN OTHER                                                00160004
                MOVE SQLCODE TO WRK-SQLCODE                             00160104
                DISPLAY 'ERRO ' WRK-SQLCODE ' NO OPEN CURSOR'           00160204
                STOP RUN                                                00160304
             END-EVALUATE.                                              00160404
      *----------------------------------------------------             00160500
       1000-99-FIM.                                 EXIT.               00160600
      *----------------------------------------------------             00160700
      *----------------------------------------------------             00160804
       4000-LER-FUNCIONARIO                       SECTION.              00160904
      *----------------------------------------------------             00161004
                                                                        00161104
            EXEC SQL                                                    00161204
             FETCH CFUNC                                                00161304
              INTO :DB2-ID,                                             00161404
                   :DB2-NOME,                                           00161504
                   :DB2-SETOR,                                          00161604
                   :DB2-SALARIO,                                        00161704
                   :DB2-DATAADM,                                        00161804
                   :DB2-EMAIL    :WRK-NULL-EMAIL                        00161904
            END-EXEC.                                                   00162004
                                                                        00162104
            EVALUATE SQLCODE                                            00162204
             WHEN 0                                                     00162304
                CONTINUE                                                00162405
             WHEN 100                                                   00163404
                DISPLAY ' '                                             00163504
                DISPLAY 'FIM DE TABELA ' DB2-ID                         00163606
             WHEN OTHER                                                 00163704
                MOVE SQLCODE TO  WRK-SQLCODE                            00163804
                DISPLAY ' '                                             00163904
                DISPLAY 'ERRO NA LEITURA' WRK-SQLCODE                   00164004
            END-EVALUATE.                                               00164104
      *----------------------------------------------------             00164204
       4000-99-FIM.                                  EXIT.              00164304
      *----------------------------------------------------             00164404
      *****************************************************             00164504
      *               P R O C E S S A R                   *             00164604
      *****************************************************             00164704
      *----------------------------------------------------             00164804
       2000-PROCESSAR                             SECTION.              00164904
      *----------------------------------------------------             00165004
            MOVE DB2-SALARIO TO WRK-SALARIO-ED.                         00165106
                                                                        00165306
            PERFORM 4500-IMPRIME-DADOS.                                 00167506
            PERFORM 4600-MAIOR-SALARIO.                                 00168006
                                                                        00168106
            ADD 1 TO WRK-REG-LIDOS.                                     00168206
            ADD DB2-SALARIO TO WRK-SALARIO-TOTAL                        00168306
                                                                        00168406
            PERFORM 4000-LER-FUNCIONARIO.                               00168506
                                                                        00168606
      *----------------------------------------------------             00168706
       2000-99-FIM.                                EXIT.                00168806
      *----------------------------------------------------             00168906
      *----------------------------------------------------             00169006
       4500-IMPRIME-DADOS                          SECTION.             00169106
      *----------------------------------------------------             00169206
            DISPLAY '.............................'.                    00169306
            DISPLAY 'ID      : ' DB2-ID.                                00169406
            DISPLAY 'NOME    : ' DB2-NOME.                              00169506
            DISPLAY 'SETOR   : ' DB2-SETOR.                             00169606
            DISPLAY 'SALARIO : ' WRK-SALARIO-ED.                        00169706
            DISPLAY 'DATAADM : ' DB2-DATAADM.                           00169806
                                                                        00169906
            IF WRK-NULL-EMAIL = 0                                       00170006
              DISPLAY 'EMAIL   : ' DB2-EMAIL                            00170106
              DISPLAY ' '                                               00170206
            ELSE                                                        00170306
              DISPLAY 'EMAIL NULO'                                      00170406
              DISPLAY ' '                                               00170506
            END-IF.                                                     00170606
      *----------------------------------------------------             00170706
       4500-99-FIM.                                EXIT.                00170806
      *----------------------------------------------------             00170906
      *----------------------------------------------------             00171006
       4600-MAIOR-SALARIO                          SECTION.             00171106
      *----------------------------------------------------             00171206
            IF DB2-SALARIO GREATER WRK-MAIOR-VALOR                      00171506
              MOVE DB2-SALARIO TO WRK-MAIOR-VALOR                       00171606
            END-IF.                                                     00171706
      *----------------------------------------------------             00171806
       4600-99-FIM.                                EXIT.                00171906
      *----------------------------------------------------             00172006
      *****************************************************             00172106
      *               F I N A L I Z A R                   *             00172206
      *****************************************************             00172306
      *----------------------------------------------------             00172406
       3000-FINALIZAR                             SECTION.              00172506
      *----------------------------------------------------             00172606
                                                                        00172706
            COMPUTE WRK-MEDIA = WRK-SALARIO-TOTAL / WRK-REG-LIDOS.      00172806
            MOVE WRK-SALARIO-TOTAL TO WRK-SALARIO-TOT-ED.               00172906
            MOVE WRK-MEDIA TO WRK-MEDIA-ED.                             00173006
            MOVE WRK-MAIOR-VALOR TO WRK-MAIOR-VALOR-ED.                 00173106
                                                                        00173206
            DISPLAY '.............................'.                    00173306
            DISPLAY ' '.                                                00173406
            DISPLAY 'REGISTROS LIDOS    : ' WRK-REG-LIDOS.              00173506
            DISPLAY 'TOTAL DOS SALARIOS : ' WRK-SALARIO-TOT-ED.         00173606
            DISPLAY 'MEDIA DOS SALARIOS : ' WRK-MEDIA-ED.               00174006
            DISPLAY 'MAIOR SALARIO      : ' WRK-MAIOR-VALOR-ED.         00174106
                                                                        00175006
            EXEC SQL                                                    00180004
              CLOSE CFUNC                                               00190004
            END-EXEC.                                                   00200004
                                                                        00201004
      *----------------------------------------------------             00210004
       3000-99-FIM.                                  EXIT.              00220004
      *----------------------------------------------------             00230004
