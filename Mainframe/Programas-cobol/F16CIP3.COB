      *====================================================             00010000
       IDENTIFICATION                            DIVISION.              00020000
      *====================================================             00030000
       PROGRAM-ID. FR16CIP3.                                            00040050
                                                                        00061009
      *====================================================*            00062009
      *   AUTOR....:LUCIANO GUIMARAES                      *            00063009
      *   ANALISTA.:IVAN SANCHES                           *            00063110
      *   DATA ....:30/06/2022                             *            00063250
      *----------------------------------------------------*            00063309
      *   OBJETIVO:REALIZA UM CALL ENTRE DOIS PROGRAMAS    *            00063450
      *            FAZ A LEITURA DOS DADOS E RETORNA NO    *            00063550
      *            CICS.                                   *            00063850
      *                                                    *            00063950
      *----------------------------------------------------*            00064010
      *   BASE DE DADOS:                                   *            00066010
      *   TABELA.VSAM                                      *            00067033
      *    ------              I/O        INCLUDE/BOOK     *            00068010
      *====================================================*            00080009
      *====================================================             00081036
       ENVIRONMENT                               DIVISION.              00082036
      *====================================================             00083036
       CONFIGURATION                              SECTION.              00090036
                                                                        00091036
       SPECIAL-NAMES.                                                   00100036
           DECIMAL-POINT IS COMMA.                                      00110036
                                                                        00110136
      *====================================================             00111036
       DATA                                      DIVISION.              00119510
      *====================================================             00119636
       WORKING-STORAGE                           SECTION.               00138001
      *----------------------------------------------------             00138101
            COPY F16CIM1.                                               00139451
            COPY DFHAID.                                                00139651
                                                                        00139710
      *----------------------------------------------------             00143610
       01 FILLER              PIC X(70) VALUE                           00143710
              '---------FILE STATUS E DADOS----------'.                 00143833
                                                                        00143944
       01 WRK-DADOS.                                                    00144051
          05 WRK-CHAVE        PIC X(05).                                00144151
          05 WRK-NOME         PIC X(30).                                00144251
          05 WRK-SETOR        PIC X(04).                                00144351
          05 WRK-SALARIO      PIC X(10).                                00144451
          05 WRK-DATAADM.                                               00144555
           10 WRK-DATADIA   PIC X(02).                                  00144655
           10 FILLER        PIC X(01) VALUE '/'.                        00144755
           10 WRK-DATAMES   PIC X(02).                                  00144855
           10 FILLER        PIC X(01) VALUE '/'.                        00144955
           10 WRK-DATAANO   PIC X(04).                                  00145055
          05 WRK-EMAIL        PIC X(40).                                00145351
          05 WRK-STATUS       PIC X(01).                                00145451
                                                                        00145551
       77 WRK-RESP            PIC S9(04) COMP.                          00145651
       77 WRK-RET-MAPA        PIC S9(04) COMP.                          00145751
       77 WRK-FASE     PIC 9(2).                                        00145856
       77 WRK-SAIDA           PIC X(40) VALUE '--FIM DE PROGRAMA--'.    00145954
                                                                        00146934
      *====================================================             00153000
       PROCEDURE                                 DIVISION.              00154000
      *====================================================             00155000
      *****************************************************             00155100
      *          R O T I N A   P R I N C I P A L          *             00155200
      *****************************************************             00155300
      *----------------------------------------------------             00156000
       0000-PRINCIPAL                            SECTION.               00157000
      *----------------------------------------------------             00157100
             PERFORM 1000-INICIALIZAR.                                  00157353
             IF EIBCALEN NOT EQUAL 1                                    00157554
                PERFORM 2000-PROCESSAR                                  00157651
             END-IF.                                                    00157751
             PERFORM 3000-FINALIZAR.                                    00157951
                                                                        00158044
             EXEC CICS                                                  00158144
               RETURN TRANSID('T163')                                   00158251
             END-EXEC.                                                  00158344
      *----------------------------------------------------             00158451
       0000-99-FIM.                               EXIT.                 00158700
      *----------------------------------------------------             00158800
      *****************************************************             00162325
      *             I N I C I A L I Z A R                 *             00162425
      *****************************************************             00162525
      *----------------------------------------------------             00166527
       1000-INICIALIZAR                           SECTION.              00166627
      *----------------------------------------------------             00166727
            IF EIBCALEN EQUAL 0                                         00166856
                EXEC CICS SEND                                          00166956
                   MAPSET('F16CIM1')                                    00167056
                   MAP('MAPA01')                                        00167156
                    ERASE                                               00167256
                   MAPONLY                                              00167356
                END-EXEC                                                00167456
                                                                        00167556
               MOVE 1 TO WRK-FASE                                       00167656
                                                                        00167756
               MOVE ZEROS TO IDO                                        00167856
               EXEC CICS SEND                                           00168056
                  MAPSET('F16CIM1')                                     00168156
                  MAP('MAPA01')                                         00168256
                  DATAONLY                                              00168356
                  FROM(MAPA01O)                                         00168456
               END-EXEC                                                 00168556
               END-IF.                                                  00168656
      *----------------------------------------------------             00168956
       1000-99-FIM.                                 EXIT.               00169027
      *----------------------------------------------------             00169127
      *----------------------------------------------------             00169227
      *****************************************************             00169327
      *               P R O C E S S A R                   *             00169427
      *****************************************************             00170027
      *----------------------------------------------------             00171727
       2000-PROCESSAR                             SECTION.              00171827
      *----------------------------------------------------             00171927
            EXEC CICS RECEIVE                                           00172054
               MAPSET('F16CIM1')                                        00172154
               MAP('MAPA01')                                            00172254
               INTO(MAPA01I)                                            00172354
               RESP(WRK-RET-MAPA)                                       00172454
            END-EXEC.                                                   00172554
                                                                        00172654
            EVALUATE EIBAID                                             00172751
                                                                        00172851
      *--SAIDA-F10                                                      00172952
             WHEN ':'                                                   00173054
               EXEC CICS SEND                                           00174051
                 FROM(WRK-SAIDA)                                        00175051
                 ERASE                                                  00176051
               END-EXEC                                                 00176151
                                                                        00176251
               EXEC CICS                                                00176351
                 RETURN                                                 00176451
               END-EXEC                                                 00176551
                                                                        00176651
      *--LIMPAR-ESPACO                                                  00176752
             WHEN  ''''                                                 00176854
               INITIALIZE MAPA01O                                       00176951
                                                                        00177051
      *--CONSULTA-F5                                                    00177152
             WHEN '5'                                                   00177254
               MOVE IDI TO WRK-CHAVE                                    00177351
                                                                        00177451
               EXEC CICS LINK                                           00177551
                  PROGRAM('F16COVSA')                                   00177653
                  COMMAREA(WRK-DADOS)                                   00177751
                  RESP(WRK-RESP)                                        00177851
               END-EXEC                                                 00177951
                                                                        00178051
               EVALUATE  WRK-STATUS                                     00178151
                WHEN '1'                                                00178251
                 MOVE WRK-NOME     TO NOMEO                             00178351
                 MOVE WRK-SETOR    TO SETORO                            00178451
                 MOVE WRK-SALARIO  TO SALARIOO                          00178553
                 MOVE WRK-DATADIA  TO DATADIAO                          00178654
                 MOVE WRK-DATAMES  TO DATAMESO                          00178754
                 MOVE WRK-DATAANO  TO DATAANOO                          00178854
                 MOVE WRK-EMAIL    TO EMAILO                            00178951
                 MOVE 'ENCONTRADO' TO MSGO                              00179051
                WHEN '2'                                                00179151
                 MOVE 'NAO ENCONTRADO ' TO MSGO                         00179251
               END-EVALUATE                                             00179351
            END-EVALUATE.                                               00179452
                                                                        00179551
                                                                        00179651
      *----------------------------------------------------             00179732
       2000-99-FIM.                                EXIT.                00179832
      *----------------------------------------------------             00180006
      *----------------------------------------------------             00190052
       3000-FINALIZAR                             SECTION.              00200053
      *----------------------------------------------------             00210052
                 EXEC CICS SEND                                         00255053
                    MAPSET('F16CIM1')                                   00256053
                    MAP('MAPA01')                                       00260053
                    DATAONLY                                            00270053
                    FROM(MAPA01O)                                       00280053
                 END-EXEC.                                              00290056
      *----------------------------------------------------             00340052
       3000-99-FIM.                                 EXIT.               00350053
      *----------------------------------------------------             00360052
