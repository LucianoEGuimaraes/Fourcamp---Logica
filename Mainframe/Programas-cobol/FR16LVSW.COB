      *====================================================             00010000
       IDENTIFICATION                            DIVISION.              00020000
      *====================================================             00030000
       PROGRAM-ID. FR16LVSR.                                            00040032
      *====================================================             00061000
      *             T R E I N A M E N T O                 *             00062000
      *====================================================             00063000
      * PROGRAMA......: FR16LVSR.                         *             00064032
      *---------------------------------------------------*             00066000
      * PROGRAMADOR...: LUCIANO GUIMARAES.                *             00067000
      * DATA..........: 27/06/2022                        *             00068030
      * EMPRESA.......: FOURSYS                           *             00069000
      * INSTRUTOR.....: IVAN SANCHES                      *             00069100
      *---------------------------------------------------*             00069200
      * OBJETIVO......: LEITURA DE UM ARQUIVO VSAM E DISP-*             00069332
      *                -LAY DE DADOS DO FUNCIONARIO LIDO  *             00069433
      *                 PELA SYSIN.                       *             00070033
      *---------------------------------------------------*             00080031
      *  ARQUIVOS   :                                     *             00090031
      *   DDNAME          I/O             INCLUDE/BOOK    *             00100031
      *   ARQVSAM         I/O                   -         *             00101040
      *---------------------------------------------------*             00101132
      *  MODULOS    :                                     *             00101232
      *===================================================*             00102031
      *====================================================             00110000
       ENVIRONMENT                               DIVISION.              00120000
      *====================================================             00130000
       CONFIGURATION                             SECTION.               00131000
       SPECIAL-NAMES.                                                   00132000
           DECIMAL-POINT  IS COMMA.                                     00133000
                                                                        00133112
       INPUT-OUTPUT                              SECTION.               00133212
       FILE-CONTROL.                                                    00133312
            SELECT ARQVSAM ASSIGN TO ARQVSAM                            00133432
                ORGANIZATION IS INDEXED                                 00133532
                ACCESS MODE  IS DYNAMIC                                 00133632
                RECORD KEY   IS ARQ-CHAVE                               00133732
                FILE STATUS  IS FS-ARQVSAM.                             00133832
                                                                        00134000
      *====================================================             00135000
       DATA                                      DIVISION.              00136000
      *====================================================             00137000
       FILE                                      SECTION.               00137113
      *----------------------------------------------------             00137213
       FD ARQVSAM.                                                      00137332
                                                                        00137613
      *-LRECL: 99-*                                                     00137732
       01 REG-ARQVSAM.                                                  00137932
          05 ARQ-CHAVE        PIC X(05).                                00138032
          05 ARQ-NOME         PIC X(30).                                00138632
          05 ARQ-SETOR        PIC X(04).                                00138732
          05 ARQ-SALARIO      PIC X(10).                                00138832
          05 ARQ-DATAADM      PIC X(10).                                00138932
          05 ARQ-EMAIL        PIC X(40).                                00139032
                                                                        00139132
      *----------------------------------------------------             00139213
       WORKING-STORAGE                           SECTION.               00139301
      *----------------------------------------------------             00139401
                                                                        00139526
      *----------------------------------------------------             00139626
       01 FILLER              PIC X(70) VALUE                           00139726
              '---------VARIAVEL SYSIN-----------'.                     00139840
                                                                        00139940
       01 WRK-SYSIN.                                                    00140040
         05 WRK-CHAVE         PIC X(05) VALUE SPACES.                   00140140
         05 FILLER            PIC X(94) VALUE SPACES.                   00141040
                                                                        00141228
      *----------------------------------------------------             00142137
       01 FILLER              PIC X(70) VALUE                           00142237
              '---------GRAVALOG E ERROS---------'.                     00142338
                                                                        00142437
           COPY '#GLOG'.                                                00142534
                                                                        00142634
       77 WRK-ERRORLOG        PIC X(08) VALUE 'ERRORLOG'.               00142740
       77 WRK-ERRO-CLOSE      PIC X(13) VALUE 'ERRO NO CLOSE'.          00142840
       77 WRK-ERRO-READ       PIC X(18) VALUE 'REGISTRO EXISTENTE'.     00142940
       77 WRK-ERRO-OPEN       PIC X(12) VALUE 'ERRO NO OPEN'.           00143140
                                                                        00143238
      *----------------------------------------------------             00143338
       01 FILLER              PIC X(70) VALUE                           00143438
              '---------MASCARAS E EDICAO--------'.                     00143538
                                                                        00143638
       77 WRK-SALARIO-ED      PIC Z.ZZZ.ZZZ.ZZZ.                        00143738
       77 WRK-SPACES          PIC X(20) VALUE SPACES.                   00143838
       77 WRK-PONTILHADOS     PIC X(24) VALUE ALL '-'.                  00143938
       77 WRK-CABECALHO       PIC X(24) VALUE                           00144038
                                             '|GESTAO DE FUNCIONARIOS|'.00144138
       77 WRK-RODAPE          PIC X(24) VALUE                           00144238
                                             '--FIM DE PROCESSAMENTO--'.00144338
      *----------------------------------------------------             00145038
       01 FILLER              PIC X(70) VALUE                           00150015
              '---------FILE STATUS--------------'.                     00150118
                                                                        00150315
       77 FS-ARQVSAM          PIC 9(02).                                00151232
                                                                        00152014
      *====================================================             00153000
       PROCEDURE                                 DIVISION.              00154000
      *====================================================             00155000
      *****************************************************             00155100
      *          R O T I N A   P R I N C I P A L          *             00155200
      *****************************************************             00155300
      *----------------------------------------------------             00156000
       0000-PRINCIPAL                            SECTION.               00157000
      *----------------------------------------------------             00157100
            PERFORM 1000-INICIALIZAR                                    00157214
            PERFORM 2000-PROCESSAR.                                     00157334
            PERFORM 3000-FINALIZAR.                                     00157414
      *----------------------------------------------------             00158200
       0000-99-FIM.                               EXIT.                 00158300
      *----------------------------------------------------             00158400
      *****************************************************             00158500
      *             I N I C I A L I Z A R                 *             00158600
      *****************************************************             00158700
      *----------------------------------------------------             00158800
       1000-INICIALIZAR                           SECTION.              00158900
      *----------------------------------------------------             00159000
            OPEN I-O ARQVSAM.                                           00159432
                                                                        00159534
            IF FS-ARQVSAM NOT EQUAL 00                                  00159637
             PERFORM 5100-ERRO-OPEN                                     00159738
            ELSE                                                        00160138
             PERFORM 5000-CABECALHO                                     00160234
             ACCEPT WRK-SYSIN FROM SYSIN                                00160340
             MOVE   WRK-CHAVE  TO   ARQ-CHAVE                           00160440
            END-IF.                                                     00160537
                                                                        00160634
      *----------------------------------------------------             00160700
       1000-99-FIM.                                 EXIT.               00160800
      *----------------------------------------------------             00161000
      *****************************************************             00163606
      *               P R O C E S S A R                   *             00163706
      *****************************************************             00163806
      *----------------------------------------------------             00163906
       2000-PROCESSAR                             SECTION.              00164006
      *----------------------------------------------------             00164106
            READ ARQVSAM                                                00164238
                                                                        00164338
            IF FS-ARQVSAM NOT EQUAL 00                                  00164438
             PERFORM 4000-WRITE-DADOS                                   00164540
            ELSE                                                        00165038
             PERFORM 5200-ERRO-READ                                     00165140
            END-IF.                                                     00165238
      *----------------------------------------------------             00167706
       2000-99-FIM.                                EXIT.                00167806
      *----------------------------------------------------             00167906
      *****************************************************             00168006
      *               F I N A L I Z A R                   *             00168106
      *****************************************************             00168206
      *----------------------------------------------------             00168306
       3000-FINALIZAR                             SECTION.              00168406
      *----------------------------------------------------             00168506
            CLOSE ARQVSAM.                                              00168932
                                                                        00169032
            IF FS-ARQVSAM NOT EQUAL 00                                  00169138
             PERFORM 5300-ERRO-CLOSE                                    00169238
            ELSE                                                        00169638
             DISPLAY WRK-SPACES                                         00169738
             DISPLAY WRK-SPACES                                         00169838
             DISPLAY WRK-PONTILHADOS                                    00169938
             DISPLAY WRK-RODAPE                                         00170038
             DISPLAY WRK-PONTILHADOS                                    00170138
             STOP RUN                                                   00170338
            END-IF.                                                     00170438
      *----------------------------------------------------             00170823
       3000-99-FIM.                                  EXIT.              00170923
      *----------------------------------------------------             00171000
      *----------------------------------------------------             00260015
       4000-WRITE-DADOS                            SECTION.             00270040
      *----------------------------------------------------             00280015
            WRITE REG-ARQVSAM FROM WRK-SYSIN                            00281240
                                                                        00281340
            IF FS-ARQVSAM NOT EQUAL 0                                   00281540
              DISPLAY 'REGISTRO NAO GRAVADO'                            00281640
            ELSE                                                        00281840
              DISPLAY 'REGISTRO GRAVADO: ' WRK-CHAVE                    00281940
            END-IF.                                                     00282040
      *----------------------------------------------------             00310021
       4000-99-FIM.                                  EXIT.              00320021
      *----------------------------------------------------             00330021
      *----------------------------------------------------             00340032
       5000-CABECALHO                             SECTION.              00350032
      *----------------------------------------------------             00360032
            DISPLAY WRK-PONTILHADOS                                     00361032
            DISPLAY WRK-CABECALHO                                       00362032
            DISPLAY WRK-PONTILHADOS                                     00363032
            DISPLAY WRK-SPACES.                                         00364032
            DISPLAY WRK-SPACES.                                         00365038
      *----------------------------------------------------             00370032
       5000-99-FIM.                                  EXIT.              00380032
      *----------------------------------------------------             00390032
      *----------------------------------------------------             00391038
       5100-ERRO-OPEN                             SECTION.              00392038
      *----------------------------------------------------             00393038
            MOVE 1000              TO WRK-SECAO                         00393138
            MOVE WRK-ERRO-OPEN     TO WRK-MENSAGEM                      00393238
            MOVE FS-ARQVSAM        TO WRK-STATUS                        00393338
            PERFORM 6000-TESTA-STATUS.                                  00393438
      *----------------------------------------------------             00398038
       5100-99-FIM.                                  EXIT.              00399038
      *----------------------------------------------------             00399138
      *----------------------------------------------------             00399238
       5200-ERRO-READ                             SECTION.              00399338
      *----------------------------------------------------             00399438
            MOVE 2000              TO WRK-SECAO                         00399538
            MOVE WRK-ERRO-READ     TO WRK-MENSAGEM                      00399638
            MOVE FS-ARQVSAM        TO WRK-STATUS                        00399738
            PERFORM 6000-TESTA-STATUS.                                  00399838
      *----------------------------------------------------             00399938
       5200-99-FIM.                                  EXIT.              00400038
      *----------------------------------------------------             00400138
      *----------------------------------------------------             00400238
       5300-ERRO-CLOSE                            SECTION.              00400338
      *----------------------------------------------------             00400438
            MOVE 3000              TO WRK-SECAO                         00400538
            MOVE WRK-ERRO-CLOSE    TO WRK-MENSAGEM                      00400638
            MOVE FS-ARQVSAM        TO WRK-STATUS                        00400738
            PERFORM 6000-TESTA-STATUS.                                  00400838
      *----------------------------------------------------             00400938
       5300-99-FIM.                                  EXIT.              00401038
      *----------------------------------------------------             00401138
      *----------------------------------------------------             00402038
       6000-TESTA-STATUS                          SECTION.              00410034
      *----------------------------------------------------             00420032
            MOVE 'FR16LVSW'   TO    WRK-PROGRAMA.                       00420140
            CALL WRK-ERRORLOG USING WRK-DADOS.                          00420237
            DISPLAY WRK-DADOS.                                          00420338
            GOBACK.                                                     00420437
      *----------------------------------------------------             00430032
       6000-99-FIM.                                  EXIT.              00440034
      *----------------------------------------------------             00450032
